sub ask_qmail {
  my($config) = @_;
  my($def, $msg);

  #---- Ask about .qmail-default files
  $msg = retr_msg('qmail_default', $lang);
  $def = (defined $config->{'maintain_mtaconfig'} ?
	  $config->{'maintain_mtaconfig'} : 1);
  $config->{'maintain_mtaconfig'} = get_bool($msg, $def);

  #---- Ask the location of the binaries
  $msg = retr_msg('qmail_binaries', $lang);
  $def = ($config->{'qmail_path'} ||
	  (-d '/var/qmail/bin' && '/var/qmail/bin') ||
	  (-d '/usr/sbin'      && '/usr/sbin'));
  $config->{'qmail_path'} = get_dir($msg, $def);
}

sub ask_qmail_domain {
  my($config, $dom) = @_;
  my($def, $msg);

  $msg = retr_msg('qmail_dir', $lang, 'DOMAIN' => $dom);
  $def = $config->{'domain'}{$dom}{'qmaildir'};
  $config->{'domain'}{$dom}{'qmaildir'} = get_dir($msg, $def);

  $msg = retr_msg('qmail_file', $lang, 'DOMAIN' => $dom);
  $def = $config->{'domain'}{$dom}{'qmailfile'};
  $config->{'domain'}{$dom}{'qmailfile'} = get_file($msg, $def);

  $config->{mta_separator} = '-';
}

sub setup_qmail {};

sub setup_qmail_domain {
  my($config, $dom) = @_;
  my($dir, $file, $line, $whoami);

  $dir    = $config->{domain}{$dom}{qmaildir};
  $file   = $config->{domain}{$dom}{qmailfile};
  $whoami = $config->{domain}{$dom}{whoami};
  $whoami =~ s/\@.*$//;

  # If file exists, open and read it.  If first line is the proper
  # comment, erase it else don't touch it and exit.
  if (-f "$dir/$file") {
    open QD, "$dir/$file"
      or die "Cannot open qmail file $dir/$file: $!";
    $line = <QD>;
    return unless $line =~ /^\# This file is automatically generated by Majordomo/;
    if (-f "$dir/$file.bak") {
      unlink "$dir/$file.bak"
        or die "Cannot unlink old qmail file $dir/$file.bak: $!";
    }
    rename "$dir/$file", "$dir/$file.bak"
      or die "Cannot rename $dir/$file to $dir/$file.bak: $!";
  }

  # Open the file, write it out and return
  open QD, ">$dir/$file"
    or die qq(Cannot open qmail file "$dir/$file": $!);
  print QD "# This file is automatically generated by Majordomo.\n";
  print QD retr_msg('qmail_comments', $lang);

  if ($config->{queue_mode}) {
    print QD "|$config->{'install_dir'}/bin/mj_enqueue -d $dom -Q $whoami\n";
  }
  else {
    print QD "|$config->{'install_dir'}/bin/mj_email -d $dom -Q $whoami\n";
  }
  close QD;
}

=head1 COPYRIGHT

Copyright (c) 1999, 2002 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

1;

#
### Local Variables: ***
### cperl-indent-level:2 ***
### cperl-label-offset:-1 ***
### indent-tabs-mode: nil ***
### End: ***
