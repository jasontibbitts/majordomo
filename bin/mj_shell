#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = "/path/to/lib";
  $::LISTDIR= "/path/to/lists";
  $::LOCKDIR= "blah";
  $::WTMPDIR= "/tmp";
  $::UMASK  = "007";
  $::UID    = "baduid";
  $::GID    = "badgid";
#  $::TMPDIR = "/tmp";
#  $Exporter::Verbose = 1;
#  $AutoLoader::Verbose = 1;
#  $AutoLoader::Notruncate = 1;

# Uncomment this to get a backtrace for every warning
#  use Carp qw(cluck);
#  $SIG{__WARN__} = sub {cluck "--== $_[0]"};

# Or this one to get plain warnings
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};
}

#print join("\n", keys(%INC)), "\n";

use lib "$::LIBDIR";
use strict;
no strict "refs";
use IO::File;
use Majordomo;
use Mj::Log;
use Mj::Format;
use Mj::Parser;
use Mj::CommandProps qw(:command :function);
use Net::Domain qw(hostfqdn);
use Getopt::Long;

use vars qw(%opts $log_entries $log_level $program_name);
my (@cmdfiles, @in, $disp, $dom, $editor, $fh, $go, $i, $id, $infile, $infh,
    $lang, $list, $mess, $ok, $passw, $result, $tempfile, $tlist, $tmpg,
    $tmpu, $top);

$| = 1;
$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

umask oct($::UMASK);

# Options:
#  -D : enable debugging to STDOUT
#  -d : specify domain to operate on
#  -F : parse commands from given file (multiple files may be used)
#  -f : give attachment file
#  -l : choose language
#  -P : use a pager to display the results
#  -p : specify password
#  -R : dump requests sent to core and results received from core
#  -t : specify directory containing domains
#  -u : specify user (for testing only)
#  -Z : For testing: wimp put of UID/GID tests
#  --id : print UID and GIDs.
#  --lockdir : specify the lock directory.  Only valid if -Z is set and the
#    real and effective UIDs match (i.e. we're not running setuid).
# --noornaments : in interactive mode, don't dress up the prompt.

Getopt::Long::config('no_ignore_case', 'bundling');

%opts = (ornaments => 1, aliascomplete => 0);
GetOptions(\%::opts, 'D', 'd=s', 'F=s' => \@cmdfiles, 'f=s@', 'l=s@',
           'P', 'p=s', 'R', 't=s', 'u=s', 'Z', 'id',
           'lockdir=s', 'ornaments!', 'aliascomplete!');

#print Dumper \%opts;

if ($::opts{'id'}) {
  print "Eff. UID: $>\n";
  print "Real UID: $<\n";
  print "Eff. GID: $(\n";
  print "Real GID: $)\n";
}

unless ($opts{'Z'}) {
  die("Not running as UID $::UID") unless $> == $::UID;
  die("Not running as GID $::GID") unless $) == $::GID;
}

if ($opts{'lockdir'} && $opts{'Z'}) {
  $opts{lockdir} =~ /(.*)/;
  ;$::LOCKDIR = $1; # Wierdness to prevent installbin expansion.
  if ($> != $< || $) != $( || -u $0) {
    die("Can't specify lockdir when running setuid!");
  }
}

# Open a log
$::log = new Mj::Log;
$::log->add
  (
   method      => 'syslog',
   id          => 'mjs',
   level       => 50,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

if ($opts{D}) {
  $::log->add	
    (	
     method      => 'handle',
     id          => 'mjs',
     handle      => \*STDERR,
     level       => 5000,
     subsystem   => 'mail',
     log_entries => 1,
     log_exits   => 1,
     log_args    => 1,
    );
};

if ($opts{R}) {
  require Data::Dumper;
  import Data::Dumper qw(Dumper);
}

if ($opts{P}) {
  $disp = 'pager';
}
else {
  $disp = '';
}

$::log->in(20, undef, "info", "Majordomo Shell client - ".scalar(localtime));
$::log->startup_time();
$::log->message(20, 'info', 'Umask set to '.sprintf('%lo', oct($::UMASK)));

# Untaint the top directory and the domain.  It may not be completely safe
# to do this.
$top = $opts{t} || $::LISTDIR;
$dom = $opts{d};
$top =~ /(.*)/;
$top = $1;

unless ($dom) {
  ($dom) = Majordomo::domains($::LISTDIR);
}
$dom =~ /([a-zA-Z0-9\-\.]+)/;
$dom = $1;

$lang = '';
$lang = join(',',@{$opts{l}}) if $opts{l};

$passw = $opts{p};
if (defined $passw) {
  $passw =~ /(.*)/; $passw = $1;
}
else {
  $passw = '';
}


# Deal with the file argument
if ($opts{f}) {
  for (my $i = 0; $i < @{$opts{f}}; $i++) {
    if ($opts{f}[$i] eq '-') {
      $in[$i] = IO::Handle->new;
      $in[$i]->fdopen(fileno(STDIN),"r");
      die "Can't open STDIN??? $!" unless $in[$i];
    }
    else {
      privs_user();
      $in[$i] = IO::File->new($opts{f}[$i]);
      privs_mj();
      die "Can't open $opts{f}[$i]: $!" unless $in[$i];
    }
  }
}

$::mj = new Majordomo $top, $dom, 1;

unless (ref $::mj) {
  die $::mj;
}

my $mj = $::mj;
my $host = hostfqdn;
my $user = $opts{'u'} || "$ENV{LOGNAME}\@$host";
$user =~ /(.*)/; $user = $1;

($id, $mess) = $mj->connect('shell', "\$LOGNAME  = $ENV{LOGNAME}
\$SHELL    = $ENV{SHELL}
\$HOME     = $ENV{HOME}
",
			    $user);

unless ($id) {
  print STDERR "Cannot connect:\n$mess";
  exit 1;
}


# If we're going to parse from a file instead of from arguments, do the
# parsing now.
if (@cmdfiles) {
  for $i (@cmdfiles) {
    privs_user();
    $infh = IO::File->new($i);
    privs_mj();
    die "Can't open $i: $!" unless $infh;
    $result = parse_file($infh, $i, \@in, $user, $passw);
  }
}

# If we don't have any arguments left and we weren't given a command file,
# we're to run interactively
elsif (@ARGV == 0) {
  parse_interactive(\@in, $passw, $disp);
}
# Else we're to interpret the command line
else {
  ($go, $result) = parse_command(shift @ARGV, (join(' ', @ARGV)), $passw,
                                 { 'user' => $user, 'display' => $disp });
}

$::log->message(50, "info", "-----Calling destructors-----");
undef $mj;
undef $::mj;
$::log->out;
if ($result) {
  exit 0;
}
exit 1;

# A simple interactive routine.
sub parse_interactive {
  my ($in, $pass, $disp) = @_;
  my ($args, @arglist, $attr, $cmd, $cmdline, $contd, $defaults, $features, 
      $go, $hereline, $hist, $lib, $needmore, $prompt, $res, $stoptag, 
      $term, $tmp);

  $^W = 0;
  require Term::ReadLine;
  $term = Term::ReadLine->new('Interactive Majordomo Shell Interface');
  $^W = 1;

  $prompt   = "Majordomo>";
  $features = $term->Features;
  $lib      = $term->ReadLine;
  $lib      =~ s/.+::(\w+)$/$1/;

  # Do not store lines into the history buffer automatically.  With
  # multi-line commands, save the whole command as one history entry.
  $term->MinLine(0);

  $term->ornaments(1) if $features->{ornaments} && $opts{ornaments};

  # Support command, list, and configuration setting completion.
  if ($features->{'attribs'}) {
    $attr = $term->Attribs();
    $attr->{'completion_function'} = \&completer;
  }

  # values from default commands;
  $defaults = {
               'delay'    => 0,
               'display'  => $disp,
               'password' => $pass,
               'user'     => $user,
              };

  # use Data::Dumper; print Dumper $features;
  
  print <<EOM;
Entering Majordomo interactive mode (using the $lib library).
Use the 'help command_line' command to get instructions.
Use the 'end' command to exit.

EOM

  while (defined($cmdline = $term->readline($prompt))) {
    $i = $/; $/ = $i; # Silence weird Perl bug
    $hist = "$cmdline\n";

    @arglist = ();
    # Process multi-line commands.  It must be possible to execute
    # the commands directly from the history buffer, as well as
    # from the command prompt.
    #
    # Process continuation lines.
    if ($cmdline =~ /[^\\]\\(\n|$)/) {
      if ($1 ne "\n") {
        $cmdline =~ s/\\$/ /;
        $needmore = 1;
      }
      else {
        $cmdline = join (" ", split (/\\\n/ , $cmdline));
        if ($cmdline =~ s#([^\\])\\$#$1#) {
          $needmore = 1;
        }
      }
      while ($needmore) {
        $hereline = $term->readline("> ");
        $hist .= "$hereline\n";
        if ($hereline =~ s#([^\\])\\$#$1 #) {
        }
        else {
          $needmore = 0;
        }
        $cmdline .=  $hereline;
      }
    }

    # Trim leading and trailing whitespace and remove tabs
    $cmdline =~ s/^\s*(.*?)\s*$/$1/;
    $cmdline =~ s/\t/ /g;

    # Change a trailing, escaped backslash to a simple backslash
    $cmdline =~ s/\\\\$/\\/;

    # Process here documents.
    if ($cmdline =~ /^([^\n]+)\s+\<\<\s*([A-Z]{3}[A-Z0-9]*)(\n|$)/) {
      $hereline = $cmdline;
      $cmdline = $1;
      $stoptag = $2;
      $needmore = ($3 ne "\n");
      unless ($needmore) {
        @arglist = split "\n", $hereline;
        shift @arglist;
        if ($arglist[$#arglist] =~ /^$stoptag$/) {
          pop @arglist;
        }
        for ($i = 0 ; $i < scalar @arglist ; $i++) {
          # Change a trailing, escaped backslash to a simple backslash
          $arglist[$i] =~ s/\\\\$/\\/;
        }
      }
      # here document
      $contd = 0;
      while ($needmore) {
        $hereline = $term->readline("> ");
        $hist .= "$hereline\n";
        last if $hereline =~ /^$stoptag$/;
        if ($contd) {
          $tmp = pop @arglist;
          $hereline = $tmp . $hereline;
        }
        if ($hereline =~ s#([^\\])\\$#$1 #) {
          $contd = 1;
        }
        else {
          # Change a trailing, escaped backslash to a simple backslash
          $hereline =~ s/\\\\$/\\/;
          $contd = 0;
        }
        push @arglist, $hereline;
      }
    }

    print "\n";
    chomp($hist);
    $term->addhistory($hist);
    ($cmd, $args) = $cmdline =~ /(\S+)\s*(.*)/;
    if ($cmd) {
      ($go, $res) = parse_command($cmd, $args, '', $defaults, @arglist);
      return 1 unless $go;
    }
    print "\n";
  }
  1;
}

# Parse a command line; returns ($go, $result); if $go is true, user has
# not requested an exit.
sub parse_command {
  my $command = shift;
  my $args    = shift;
  my $pass    = shift;
  my $dfl     = shift;
  my @arglist = @_;
  my ($display, $list, $mode, $prog, $request, $sublist, $tlist);
  $pass ||= '';  $pass =~ /(.*)/; $pass = $1;

  unless ($command) {
    print STDERR "**** No command specified!\n";
    return (1, 0);
  }

  ($command, $mode) = $command =~ /([^=-]+)[=-]?(.*)/;
  $mode ||= "";

  my $true_command = command_legal($command);
  unless (defined($true_command) &&
	  command_prop($true_command, "shell"))
    {
      print STDERR "**** Illegal command: $command!\n";
      return (1, 0);
    }

  if ($true_command eq 'end') {
    print "Exiting.\n"; return (0, 0);
  }
  if ($true_command eq 'approve') {
    ($pass, $command, $args) = $args =~ /(\S+)\s*(\S*)\s*(.*)/;
    print STDERR "**** approve requires a password!\n"
      unless $pass;
    return parse_command($command, $args, $pass, $dfl, @arglist);
  }

  if (command_prop($true_command, "list")) {
    ($tlist, $args) = split(" ", $args, 2);
    $tlist ||= "";
    $args ||= "";
    ($list, $sublist) = $::mj->valid_list($tlist,
                                command_prop($true_command, 'all'),
                                command_prop($true_command, 'global'));

    unless (defined $list) {
      print STDERR "**** Illegal list \"$tlist\".\n";
      return (1, 0);
    }
    $list .= ":$sublist" if (length $sublist);
  }
  if ($true_command eq 'default') {
    ($command, $args) = split(" ", $args, 2);
    if ($command =~ /^password|passwd$/) {
      $dfl->{'password'} = $args || '';
      if (defined $args and length $args) {
	print STDERR "Default password set to \"$dfl->{'password'}\".\n";
      }
      else {
	print STDERR "Default password canceled.\n";
      }
    }
    elsif ($command eq 'user') {
      if (defined $args) {
        $dfl->{'user'} = $args;
      }
      else {
        $dfl->{'user'} = $user;
      }
      print STDERR "User set to \"$dfl->{'user'}\".\n";
    }
    elsif ($command eq 'delay') {
      my ($time) = time;
      if (defined $args) {
        $dfl->{'delay'} = Mj::List::_str_to_time($args) || $time;
        $dfl->{'delay'} -= $time;
      }
      else {
        $dfl->{'delay'} = 0;
      }
      print STDERR "Delay set to $dfl->{'delay'} seconds.\n";
    }
    elsif ($command eq 'display') {
      if (defined $args) {
        if ($args =~ /^page/i) {
          $dfl->{'display'} = 'pager';
        }
        elsif ($args =~ /^edit/i) {
          $dfl->{'display'} = 'editor';
        }
        else {
          print STDERR "Illegal display \"$args\".\n";
          print STDERR "Valid display values are pager and editor.\n";
        }
      }
      else {
        $dfl->{'display'} = '';
      }
      print STDERR "Display was set to $dfl->{'display'}.\n";
    }
    else {
      print STDERR "Illegal action \"$command\" for default.\n";
    }
    return 1;
  }

  if ($true_command =~ /newfaq/) {
    $args = "/faq Frequently Asked Questions";
    $true_command = "put";
  }
  elsif ($true_command =~ /newinfo/) {
    $args = "/info List Information";
    $true_command = "put";
  }
  elsif ($true_command =~ /newintro/) {
    $args = "/intro List Introductory Information";
    $true_command = "put";
  }

  if ($true_command eq "configedit" or
      $true_command eq "get" and $mode =~ /edit/) {
    $display = 'editor';
  }
  else {
    $display = $dfl->{'display'};
  }

  if ($display eq 'pager' or $display eq 'editor') {
    # Switch UIDs, open a file, restore
    privs_user();
    ($tempfile, $fh) = tempfile();
    privs_mj();
  }
  else {
    $fh = \*STDOUT;
  }

  if ($true_command eq 'configedit') {
    $true_command = 'configshow';
  }

  $request = {
    'command'   => $true_command,
    'delay'     => $dfl->{'delay'},
    'list'      => $list,
    'mode'      => $mode,
    'password'  => $pass || $dfl->{'password'},
    'user'      => $dfl->{'user'},
  };
  # XXX $in[0] is an attachment from global variable @in.
  Mj::Parser::parse_args($request, $args, \@arglist, $in[0]);
  if (function_prop($true_command, 'iter')) {
    $request->{'command'} .= '_start';
  }

  if($opts{R}) {
    print "Request:\n";
    print Dumper($request);
  }
  $result = $mj->dispatch($request);
  if ($opts{R}) {
    print "Results:\n";
    print Dumper($result);
  }

  &{"Mj::Format::$true_command"}($mj, $fh, $fh, 'text', $request, $result);

  # If the default display command or -P option was used, display
  # the output of the command in the appropriate external program.
  #
  if ($display eq 'pager' or $display eq 'editor') {
    # Pull the editor out of the environment and untaint it. This is safe
    # because we only exec the editor as the calling user.
    if ($display eq 'editor') {
      $editor = ($ENV{EDITOR} || $ENV{VISUAL} 
                 || (-f '/bin/vi'     && '/bin/vi')
                 || (-f '/usr/bin/vi' && '/usr/bin/vi'));
    }
    else {
      $editor = ($ENV{PAGER}
                || (-f '/usr/bin/more' && '/usr/bin/more')
                || (-f '/bin/more'     && '/bin/more'));
    }

    $editor =~ /(.*)/;
    $editor = $1;

    $fh->close;
    if ($result) {
      my $modtime = (stat("$tempfile"))[9];
      # Switch UIDs, run the editor, restore
      privs_user();
      $ok = system("$editor $tempfile");
      privs_mj();

      if ($display eq 'editor') {
        if ($ok) {  # system has reversed semantics...
          print "Editor returned an error: $?\n";
          $ok = prompt('Abort configedit?', 'yes');
          if ($ok =~ /^n/i) {
            undef $ok; # Dirty trick
          }
        }
        # Another dirty trick; do this if the system returned a null value
        # (reversed semantics) or we cleared that after the abort prompt
        # above.
        if (!$ok) {
          if ((stat("$tempfile"))[9] != $modtime) {
            # Switch, open, restore
            privs_user();
            $fh = new IO::File "$tempfile";
            privs_mj();

            $result = parse_file($fh, '', [], $user, $pass || $dfl->{'password'});
            $fh->close;
          }
          else {
            print "\nFile unchanged; not executing.\n";
          }
        }
      }
      # just fall through if a pager was requested.
    }
    else {
      # Got bad result!
      # Switch, open, restore
      privs_user();
      $fh = new IO::File "$tempfile";
      privs_mj();

      print while <$fh>;
    }

    # Switch, delete the tempfile, restore
    privs_user();
    unlink $tempfile;
    privs_mj();
  }
  return (1, $result);
}

sub parse_file {
  my $infh = shift;
  my $file = shift;
  my $att  = shift;
  my $user = shift;
  my $pass = shift;

  my $result =
    Mj::Parser::parse_part($::mj,
			   infh        => $infh,
			   outfh       => \*STDOUT,
			   attachments => $att,
			   title       => $file,
			    'reply_to' => $user,
			    password   => $pass,
			  );
  return $result;
}

sub prompt ($;$) {
  my($mess,$def)=@_;
  my $ISA_TTY = -t STDIN && -t STDOUT ;
  Carp::confess("prompt function called without an argument")
    unless defined $mess;
  my $dispdef = defined $def ? "[$def] " : " ";
  $def = defined $def ? $def : "";
  my $ans;
  if ($ISA_TTY) {
    local $|=1;
    print "$mess $dispdef->";
    chomp($ans = <STDIN>);
  }
  return $ans || $def;
}

# Thanks to CPAN.pm for showing me how to do this
sub completer {
  my ($word, $line, $pos) = @_;
  my (@lists, @vars, @out, $ac, $pass);
  $pass = $opts{p};
  $ac   = $opts{aliascomplete};
  $word ||= "";
  $line ||= "";
  $pos ||= 0; # $pos is the position of the first letter of the word we're
              # on in the string.
  # At the beginning, any command will do
  if ($pos == 0) {
    @out = commands_matching("^\Q$word\E", $ac, ['shell']);
    return sort @out;
  }
  # If we're completing the password, no completion
  if ($line =~ /^\s*approve\s+\S*$/) {
    return ();
  }
  # Trim 'approve passwd' off
  if ($line =~ s/^(\s*approve\s+(\S+)\s*)//) {
    $pos -= length($1);
    $pass = $2;
  }
  # If now on a command, complete real commands
  if ($line =~ /^\S*$/) {
    @out = commands_matching("^\Q$word\E", $ac, ['shell', 'real']);
    return sort @out;
  }
  # If on the second word after a command, complete as a list
  if ($line =~ /^(\S+)\s*(\S*)$/ && command_prop($1, 'list'))
    {
      @lists = $mj->get_all_lists($user, $pass);
      return sort(grep(/^\Q$2\E/, @lists));
    }
  # Complete config variables.  XXX This doesn't handle completion of group
  # names, but I'm inclined to not worry about it.
  if ($line =~ /^config\S+\s+(\S+)\s+(\S*)$/) {
    @vars = $mj->config_get_vars($user, $pass, $1, 'MAIN', 'ALL');
    return sort(grep(/^\Q$2\E/, @vars));
  }
  ();
}


# Switch to and from the calling user privileges
sub privs_user {
  $tmpu = $>; $> = $<;  $tmpg = $); $) = $(;
}

sub privs_mj {
  $> = $tmpu; $) = $tmpg;
}

# Generate a temporary filename and return it and a filehandle open on it
sub tempfile {
  my $chr = 'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890_';
  my $base = "$::WTMPDIR/mjs.$$.";
  my($handle, $name);

  # Try to open a file ten times
  for (my $i = 0; $i < 10; $i++) {

    # Append ten random characters
    $name = $base;
    for (my $j = 0; $i < 10; $i++) {
      $name .= substr($chr, rand(length($chr)), 1);
    }
    $handle = new IO::File($name, O_CREAT | O_EXCL | O_RDWR, 0600);
    return ($name, $handle) if $handle;
  }
  die "Couldn't open a temporary file after ten tries: $!";
}

=head1 NAME

mj_shell - Majordomo command line interface

=head1 SYNOPSIS

B<mj_shell> [B<-D>] [B<-R>] [B<-d> domain] [B<-t> topdir] [B<-p> password] [command [args]]

=head1 DESCRIPTION

B<mj_shell> provides a command line interface to the Majordomo core
functionality.  It provides both a single command interface, where a
command is given directly on the command line, and an interface into the
full text parser, where commands are parsed out of a file.  Anything that
can be done through the email interface can be done through the shell
interface.

B<mj_shell> also provides a convenient 'configedit' command that opens an
editor on a set of config variables and allows them to be edited directly.
Similarly, the 'get-edit' command will start a text editor program
to allow a file to be edited and replaced easily.

If called without a command file, B<mj_shell> will enter an interactive
mode where you can enter commands and have their results returned without
rerunning B<mj_shell>.

=head2 Executing single commands

B<mj_shell> allows the user to execute a single command directly, just by
placing it and its arguments on the command line:

  mj_shell who mylist
  mj_shell subscribe mylist whoever@example.com

For a list of supported commands, execute

  mj_shell help topics

=head2 Passwords

The B<-p> switch specifies that the command should be executed with the
given password.  For general information on how passwords work in
Majordomo, execute

  mj_shell -P help admin_passwords

The C<-P> option will cause the help file to be displayed using an
external pager program, making it easier to read.

=head2 Command modes

The specific behavior of most of the commands can be altered through the
use of command modes.  Most commands take the 'noinform' and 'nolog' modes,
which turn off owner information and logging, respectively.  The
B<subscribe> command takes the 'nowelcome' mode, which prevents the
introductory welcome message from being sent out to new subscribers.

To specify one or more modes, separate them from the command with
hyphens, as follows:

  mj_shell -p pass subscribe-noinform-nowelcome mylist user@example.com

For information on which commands take which modes, execute

  mj_shell help admin_commands

=head2 Commands taking multiple or array arguments

Some commands, like B<subscribe>, can take multiple arguments (in this
case, a list of addresses to subscribe).  Others, like B<configset>,
sometimes take array arguments.  These are handled in B<mj_shell> by
placing the multiline part of the command in a file and specifying the file
using the B<-f> switch.  For example, if 'addr_file' contains a list of
addresses, one per line, you can do the following:

  mj_shell -p pass -f addr_file subscribe-noinform-nowelcome mylist

For another example, if the file 'inform_file' contains the following:

  subscribe   : all : ignore
  unsubscribe : all : ignore

Then

  mj_shell -p pass -f inform_file configset mylist inform

would set the inform variable such that the list owner would not be
informed of subscribe and unsubscribe requests.  (Execute

  mj_shell help configshow
  mj_shell help configset

for more information about configuration variables.)

=head2 Executing multiple commands

For performing more involved tasks, B<mj_shell> gives access to the full
Majordomo text parser, which is also used to parse commands from the body
of an email message.  Place multiple commands in a file, and pass the
filename to B<mj_shell> using the B<-F> switch.

For example, if 'cmd_file' contains the following lines:

  subscribe mylist usera@example.com
  subscribe mylist userb@example.com
  show mylist usera@example.com
  show mylist userb@example.com

all of the commands can be executed by calling B<mj_shell> as follows:

  mj_shell -p pass -F cmd_file

The text parser also allows some advanced features.  Multiline arguments
can be specified by a syntax similar to the shell 'here document' feature.
An example is simpler than a description; you could place the following in
'cmd_file':

  subscribe mylist << ENDA
  usera@example.com
  userb@example.com
  ENDA

  show mylist << ENDB
  usera@example.com
  userb@example.com
  ENDB

  configset mylist inform << AAA
  subscribe   : all : ignore
  unsubscribe : all : ignore
  AAA

to additionally set the 'inform' variable.  (In this example the syntax is
not much shorter, but for longer address lists it can save much typing.)
The '<<' begins the multiline argument, and must be at the end of the
command line.  The tag must consist of at least three uppercase letters,
and appears both directly after the '<<' and alone on a line after all of
the data.

Multiline arguments can also be included in external files.  The external
files given using one or more B<-f> options on the B<mj_shell> command
line, and their contents are referenced by placing '<@' followed by a
number; using '3' would reference the file named by the third B<-f> option.
For example, if the file 'addrs' contains:

  usera@example.com
  userb@example.com

'inform' contains:

  subscribe   : all : ignore
  unsubscribe : all : ignore

and 'commands' contains:

  subscribe mylist <@1
  show mylist <@1
  configset mylist inform <@2

then the command

  mj_shell -F commands -f addrs -f inform

would do the same as the previous example.

In addition, the text parser supports the special 'default' command, which
sets some default parameter for following commands.  The 'default list'
command (followed by a list name) sets the default list, while the 'default
password' command sets the password.  Thus the 'commands' file in the
previous example can be replaced by:

  default list mylist
  subscribe <@1
  show <@1
  configset inform <@2

The default list or password can be cleared by using 'default list' or
'default password' with no list or password.

In interactive mode, multiline arguments and the 'default delay,'
'default password,' and 'default user' commands are supported.
Use the command

  mj_shell help default

to learn more about the default command.

=head2 The configedit command

There is one command which is exclusive to B<mj_shell>, the B<configedit>
command.  This enables a list owner to retrieve the values of one or more
configuration variables, modify them in an editor, and save the new values
in a single operation.

Note that the configedit command is not supported on all platforms, but can
be emulated by the following set of operations:

  mj_shell -p pass configshow list vars > tempfile
  (edit tempfile)
  mj_shell -p pass -F tempfile

=head2 Switches

Here are all of the command line switches that B<mj_shell> accepts:

=over 4

=item B<-D>

Specifies that full debugging information will be sent to standard output.

=item B<-d> I<domain>

Specifies the domain of lists to operate on.  The default domain is chosen
at install time; for installations supporting more than one domain, this
switch must be used to distinguish between them.

=item B<-F> I<filename>

Specifies that the text parser will be run on I<filename>.  All non-option
arguments on the B<mj_shell> command line will be ignored.

=item B<-f> I<filename>

Specifies that the contents of I<filename> will be used either as a
multiline argument for a given command, or for an external argument for a
'<@' specifier in the text parser.

More than one B<-f> argument is permitted, though multiple instances are
only used by the text parser.

I<filename> can also be the string '-', in which case standard input is
read.

=item B<-l> I<language>

Specifies the user's preferred language.  This should be in the standard
two-letter (ISO 639) form, plus subtags as outlined in RFC 1766.  (For
example, B<en> for English, B<de> for German, or B<no-nynorsk> for
Norwegian/Nynorsk.)  Case is not significant.  Multiple preferences can be
specified in order by separating them with commas or using multiple B<-l>
options.

Note that the server may not have all files available in all supported
languages.  If a file does not exist in the preferred language, the other
supplied languages are searched in order and finally English is used if no
other choice is available.  In conformance with RFC 1766, tags are not
considered separately from tags, so in the Nynorsk example above, plain
Norwegian would not be returned unless it was listed as an additional
preference.

=item B<-P>

Use a pager to display the results.  It is also possible to achieve this
effect in interactive mode with the following command:

  default display pager

=item B<-p> I<password>

Specifies a password that a command will be run with, or the default
password for commands run by the text parser.

=item B<-R>

As a debugging aid, specifies that mj_shell should dump the request
structures sent to the Majordomo library and the result structures received
from the library.

=item B<-t> I<directory>

This specifies the directory which is the top level of the Majordomo lists
directory structure.  This is generally compiled in at installation time,
but some sites may have different directory structures.

=item B<-u> I<user>

Specifies the user that commands will run as.  Note that this does not
change the system UID, but only affects the user (really the email address)
that is passed to the Majordomo core for access checking and which is used
as the default for certain commands.  Normally the user is determined from
a combination of environment variables and the result of the 'hostname'
function, but this information is not always accurate.

Please note that this can be used for nefarious purposes, but that it does
not pose any additional security risk since mail sent to the email
processor can be trivially forged.  List owners are strongly urged not to
rely on users and email addresses for security, but instead to rely on
passwords and to enable confirmation on all security sensitive commands.

=item B<-Z>

Specifies that UID/GID tests will not be done.  Normally B<mj_shell> will
check to make sure that it is running as the user and group specified
during installation.  Failure of this test generally indicates an
installation error; either the wrappers or the scripts themselves shouldf
be installed setuid and setgid to the Majordomo user and group.  Using the
B<-Z> option enables B<mj_shell> to run even with this misconfiguration,
though other operations may fail and file ownerships will not be set
correctly.

This option is mainly used by the test scripts, so that they can run before
the software has been completely installed.

=item B<--aliascomplete>

When running in interactive mode, B<mj_shell> provides for completion of
Majordomo commands.  It does not normally complete command aliases (such as
'remove' for 'unsubscribe') but supplying this switch will cause it to do
so.

=item B<--id>

Forces B<mj_shell> to print out the real and effective user and group IDs
at the beginning of execution.  Used for debugging.

=item B<--noornaments>

When running in interactive mode, B<mj_shell> will use ornamentation to
spruce up the prompt of the underlying ReadLine library supports it.  The
B<--noormanemts> switch will turn these off.

=head1 COPYRIGHT

Copyright (c) 1997, 1998, 2000 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#
### Local Variables: ***
### mode:cperl ***
### cperl-indent-level:2 ***
### End: ***
