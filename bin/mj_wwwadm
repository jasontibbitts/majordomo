#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::DEFDOM = 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_wwwadm.debug");
  }
}

=head1 NAME

mj_wwwadm - demonstration of majordomo administration via the WWW.

=head1 SYNOPSIS

Extend this file to create specialized CGI scripts.

=head1 DESCRIPTION

When utilizing majordomo with a CGI script, there are
several basic steps that must be taken, including initializing
majordomo variables,  creating a log (STDERR by default), 
initializing a CGI object, and starting a majordomo session
with connect().

This script performs these basic steps, easing the 
creation of new CGI scripts.  Extend it to suit your needs.

Note that it may be necessary to install a setuid wrapper
for any script based upon this template.

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::Format;
use Mj::Log;
use Mj::TextOutput;
use Mj::Addr;

my ($cgi, $domain, $mj, $ok, $sess, $token);
my ($ofunc, $func, $user, $passw, $pathinfo, $mode, $list, $vict, $extra);
my ($line, $st, $file, $fh, $cgiurl, $cmdform, $localpart);

$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

umask oct($::UMASK);

# Change the log level to a higher number (200) for complete debugging
$::log = new Mj::Log;
$::log->add
  (
   method      => 'handle',
   id          => 'mjwww',
   handle      => \*STDERR,
   level       => 50,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

$::log->in(20, undef, "info", "Majordomo WWW Admin - ".scalar(localtime)." from $ENV{'REMOTE_ADDR'}");
$::log->startup_time();

# Start up the page and parse the URL
$cgi = new CGI;
print $cgi->header;
$domain = $::DEFDOM;

# Make the Majordomo object
$mj = new Majordomo $::LISTDIR, $domain;
$cgiurl = $mj->{'sitedata'}{'config'}{'cgi_url'} . "mj_wwwadm";

# Generate the session information
for my $i ('REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "$i = $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= scalar(localtime(time)) . "\n";
# XXX Check result
$mj->connect('mj_wwwadm', $sess);

$pathinfo = 'GLOBAL';
if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  $pathinfo =~ m#/([^/\s]+)#;
  $pathinfo = $1;
}

# GET poses a security risk due to passwords being visible
# Set HTML form variables
$ofunc   = $cgi->param('func') || "help";
$localpart = $ENV{'REMOTE_ADDR'} || "mj_wwwadm";
$user    = new Mj::Addr("$localpart\@example.com");
$passw   = $cgi->param('passw') || '';
$list    = $cgi->param('list') || $pathinfo;
$extra   = $cgi->param('extra') || '';
$st      = $cgi->param('st') || '';
if ($st eq 'on') { $st = 'checked'; }

# Extract "mode" from command name
if ($ofunc =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  $mode = $2;
}
else { 
  $func = $ofunc;
  $mode = '';
}

$cmdform = <<EOM;
<form method=POST action=$cgiurl>
command:
<select name=func>
<option> accept
<option> archive-index
<option> archive-get
<option> auxadd
<option> auxremove
<option> auxwho
<option> configshow
<option> configset
<option> configdef
<option> get
<option> help
<option> index-recursive
<option> lists-aux
<option> reject
<option> sessioninfo
<option> set
<option> showtokens
<option> subscribe
<option> subscribe-nowelcome
<option> tokeninfo
<option> unsubscribe
<option> unregister
<option> which
<option> who-enhanced
<option> about this service
</select>
<input size=24 name=extra value="$extra">
<input type=checkbox name=st $st>show pending requests<br>
password:
<input size=14 name=passw type=password value="$passw">
mailing list:
<input size=14 name=list value="$list">
<input type=submit value="Issue command">
</form>
EOM

# Select the previous command.
$cmdform =~ s/option> $ofunc/option selected> $ofunc/;

print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd"> 
<html><head>
<title>Majordomo 2 Administration: $func</title>
</head>
<body>
EOM

print $cmdform;

if (! $cgi->param('func') or $func =~ /about/) {
  &usage($mj);
  exit 0;
}

no strict 'refs';
# Make sure the command is valid
unless (defined ${Mj::TextOutput::}{$func}) {
  print "Illegal command: $func\n";
  $::log->message(50, "info",  "Invalid command $func in form data.");
  exit 0;
}

# Make sure the list name is valid
if ($list ne '') {
  unless ((-d "$mj->{'ldir'}/$list" and ($list = $mj->legal_list_name($list)))
           or $list eq 'ALL') {
    print "<h3>Invalid list name: $list</h3>\n";
    $::log->message(50, "info",  "Invalid list $list in form data.");
    exit 0; 
  }
}

# The archive-get and archive-index commands must use immediate mode,
# to display the results instead of mailing them.
if ($func eq 'archive' or $func eq 'get') {
  $mode .= "-immediate";
}

# Verify the password provided, if the command requires one.
if ($passw eq '' and $func !~ /help|lists|accept|reject|tokeninfo/) {
  print "<h3>You must provide a password to use the $func command</h3>\n";
  exit 0;
}
elsif ($passw ne '') {
  # Check the password against the requester
  $ok = $mj->validate_passwd($user, $passw, undef,
                               "mjwww", $list, $func);
  if ($ok <= 0) {
    print "<h3>The password you provided is invalid</h3>\n";
    $::log->message(50, "info",  "Invalid password in form data.");
    exit 0;
  }
}

if ($func eq 'auxadd' or $func eq 'auxremove') {
  # The group and mail address must be specified in 'extra'
  if ($extra !~ /\s/) {
    print "<p>When using $func, specify the auxiliary list name\n";
    print "and the e-mail address, in that order.\n";
    exit 0;
  }
}


$file = Majordomo::tempname();
$fh = new IO::File ">$file";
&{"Mj::TextOutput::$func"}($mj, $func, $user, 
                           ($func =~ /help|which|lists/)? '' : $passw, 
                           undef, "mjwww",
                           undef, $fh, $mode, $list, $extra, ());

if ($st eq 'checked' and $passw ne '' and $func ne 'showtokens') {
  print $fh "\n";
  # present the result of the showtokens command
  &{"Mj::TextOutput::showtokens"}($mj, $func, $user, $passw, 
                                  undef, "mjwww",
                                  undef, $fh, $mode, $list, $extra, ());
}
$fh->close();

# process results of TextOutput
&format_print($file);
unlink $file;

$::log->out;
exit 0;

#---------------- Subroutines -----------------#
sub format_print {
  my $file = shift;
  my ($line, $token, $summarize, $hdrs);
  $summarize = $hdrs = 0;

  $fh = new IO::File "<$file";
  if (! $fh) { 
    print STDOUT "<h3>Unable to get results</h3>\n";
    exit 0;
  }
  print STDOUT "<pre>\n";
  my %esc = ( '&'=>'amp', '"'=>'quot', '<'=>'lt', '>'=>'gt');
  while ($line = <$fh>) {
    $line =~ s/([<>\"&])/\&$esc{$1};/mg; 
    $line =~ s/([\x80-\xFF])/'&#'.unpack('C',$1).';'/eg;
    if ($line =~ /(Token  )(.*)/) {
      print STDOUT <<EOM;
</pre>
<form method=POST action=$cgiurl>
<input type=submit name=func value="tokeninfo-full">
<input type=submit name=func value="tokeninfo">
<input type=submit name=func value="accept">
<input type=submit name=func value="accept-archive">
<input type=submit name=func value="reject">
<input type=hidden name=st value=$st>
<input type=hidden name=list value=$list>
<input type=hidden name=passw value=$passw>
<br>
Choose one request from the list below,
then press one of the buttons above.
<pre>
EOM
      $summarize = 1;
      next;
    }
    elsif ($func eq 'tokeninfo' and $line =~ /^Information about the session /) {
      print STDOUT "<small>\n";
      $hdrs = 1;
    }
    elsif ($hdrs and $line =~ /^$/) {
      print STDOUT "</small></pre>\n";
      print STDOUT <<EOM;
<form action=$cgiurl method=POST>
<input type=submit name=func value=accept>
<input type=submit name=func value=accept-archive>
<input type=submit name=func value=reject>
<input type=hidden name=extra value='$extra'>
<input type=hidden name=list value=$list>
<input type=hidden name=passw value=$passw>
<input type=hidden name=st value=$st>
</form>
<pre>
EOM
      $hdrs = 0;
    }
    # add link to tokeninfo if a token is present
    elsif ($line =~ /^([0-9A-F]{4}-[0-9A-F]{4}-[0-9A-F]{4})/) {
      $token = $1;
      $line = "<input type=radio name=extra value=$token>$line";
    }
    print STDOUT $line;
  }
  $fh->close();
  print STDOUT "</pre>\n";
  print STDOUT "</form>\n" if $summarize;
  print STDOUT <<EOM;
</body></html>
EOM
}

sub usage {
  my ($mj) = shift;
  my $site  = $mj->_global_config_get('site_name');
  my $serveraddr = $mj->_global_config_get('whoami');
  my $serveradmin = $mj->_global_config_get('whoami_owner');

  print <<EOM;
Welcome to $site!  This is the Majordomo 2 administrative interface
for the $list mailing list.  Below you will learn how it works.
It is always possible to return to this introduction by choosing
"about this service" from the menu above and pressing the 
"Issue command" button.
<p>
At the top of this page is a form that will allow you to issue 
most of the commands you will need to administrate a mailing list.
To use it, first choose a command from the command menu.
Some commands, like "lists-aux," have a hyphen in their names.
The part that comes after the hyphen is called the "command mode."
Some of the commands may be unfamiliar to you.
<p>
To learn more about a particular command, choose "help" in 
the menu above, then type the name of the command without
the command mode in the box immediately to the right.
Finally, press the "Issue command" button, and you will
see a summary of how the command works.  You can get the
same summary by sending the <b>help</b> command in the body of
an e-mail message to 
<a href="mailto:$serveraddr">$serveraddr</a>.
<p>
Most commands require a password, so please type the password
for the $list mailing list into the <b>password:</b> box at the 
top of the page.  If you wish to administer a different list,
simply type its name into the <b>mailing list:</b> box.
<p>
The command form at the top of the page will always appear
when you issue a command.  If the <b>show pending requests</b>
button is pressed, you will also see at the bottom of the page
all of the pending requests for your list.  Requests can
be any number of things:  a subscription or set request that
one of your subscribers has not yet confirmed, a request
for help that was sent to the mailing list by mistake, or
a posted message that is held for your approval because a
header line was too long, for example.
<p>
If there are no pending requests, you will see a message like
<pre>
No tokens for $list.
</pre>
However, if there are requests available, you will see a form
that looks like this:
<form method=POST action="/cgi-bin/test-cgi">
<input type=submit name=func value="tokeninfo-full">
<input type=submit name=func value="tokeninfo">
<input type=submit name=func value="accept">
<input type=submit name=func value="accept-archive">
<input type=submit name=func value="reject">
<input type=hidden name=st value=>
<input type=hidden name=list value=$list>
<input type=hidden name=passw value=yourpassword>
<br>
Choose one request from the list below,
then press one of the buttons above.
<pre>
<input type=radio name=extra value=13D7-C445-6A37>13D7-C445-6A37 post   2000-05-12 20:27:46 Someone You Know &lt;someone\@example.com&gt;
<input type=radio name=extra value=8E49-E388-BADC>8E49-E388-BADC post   2000-05-13 10:27:46 Someone New &lt;unknown\@example.org&gt;
2 tokens shown.
</pre>
</form>
In the list of requests, the first column lists the "token ID," which 
is unique to that request.  Next is the type of request.  Generally,
<b>subscribe</b> and <b>set</b> requests need to be confirmed by
the subscribers themselves, but any other request should be handled 
by the list owners.  The remaining columns contain the date of
the request and the e-mail address of the person who made it.
<p>
There are five buttons available to deal with a pending request.
<ol>
<li>The tokeninfo-full button will show the headers of the message
that caused the request, plus the message itself if the request
is a <b>post</b>.
<li>The tokeninfo button will show the headers of the message
and a summary of the request.
<li>The accept button will approve the request.
<li>The accept-archive button will approve the request, but
if it is a <b>post</b>, the message will be stored in
the archive but not distributed to the subscribers.
<li>The reject button will deny the request.
</ol>
Requests will eventually expire unless someone handles them.
By default, the lifetime of a token is one week.
<p>
If you have questions about this interface, send mail to your
site administrator at 
<a href="mailto:$serveradmin">$serveradmin</a>.
</body>
</html>
EOM
}
=head1 COPYRIGHT

Copyright (c) 2000 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#^L
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***


