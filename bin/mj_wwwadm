#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UID    = "baduid";
  $::GID    = "badgid";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_wwwadm.debug");
  }

  # Croak if install was botched
  die("Not running as UID $::UID") unless $> == $::UID;
  die("Not running as GID $::GID") unless $) == $::GID;
  $< = $>; $( = $);
}

=head1 NAME

mj_wwwadm - demonstration of majordomo administration via the WWW.

=head1 SYNOPSIS

 The command "configshow GLOBAL wwwadm_url" will display the URL.

=head1 DESCRIPTION

When utilizing majordomo with a CGI script, there are
several basic steps that must be taken, including initializing
majordomo variables,  creating a log (STDERR by default), 
initializing a CGI object, and starting a majordomo session
with connect().

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::Addr;
use Mj::CommandProps qw(:command :function);
use Mj::Format;
use Mj::Log;
use Mj::Parser;

my (%commands, %seen, @domains, @extra, @hereargs, @lists, @mlists, 
    @settings, @values, @victims, $addr, $cgi, $cgidata, $cgiurl, 
    $cs, $debug, $domain, $end, $falseuser, $fh, $func, $i, $j, $line,
    $list, $loc, $mess, $mj, $mode, $ofunc, $ok, $opass, $passw,
    $pathinfo, $raw, $req, $request, $result, $sess, $start, $str, 
    $sublist, $subs, $tmp, $token, $truelist, $user);

#----- Which commands are supported ?  -----#
# First value:  Is a list required?
# Second value:  Page to display after the result.
%commands = (   
                'accept'        => [0, 'showtokens'],
                'archive'       => [1, 'archive'],
                'configdef'     => [1, 'configshow'],
                'configset'     => [1, 'configshow'],
                'configshow'    => [1, 'configshow'],
                'createlist'    => [0, 'newlist'],
                'digest'        => [1, 'lists'],
                'faq'           => [1, 'lists'],
                'get'           => [1, 'lists'],
                'help'          => [0, 'help'],
                'index'         => [1, 'index'],
                'info'          => [1, 'lists'],
                'intro'         => [1, 'lists'],
                'lists'         => [0, 'lists'],
                'put'           => [1, 'index'],
                'register'      => [1, 'who'],
                'reject'        => [0, 'showtokens'],
                'report'        => [1, 'lists'],
                'sessioninfo'   => [0, 'showtokens'],
                'set'           => [1, 'who'],
                'showtokens'    => [1, 'showtokens'],
                'subscribe'     => [1, 'who'],
                'tokeninfo'     => [0, 'showtokens'],
                'unregister'    => [1, 'who'],
                'unsubscribe'   => [1, 'who'],
                'which'         => [1, 'which'],
                'who'           => [1, 'who'],
            );

#----- Initialize the Log -----#
&initialize_log;

#----- Initialize the CGI object -----#
$cgi = new CGI;

#----- Obtain domain and list names  -----#
$pathinfo = '';
$domain = $cgi->param('domain') || '';

if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  while ($pathinfo =~ s#/domain=([^/]+)##) {
    $tmp = $1;
    next if ($tmp =~ /[^a-zA-Z0-9.-]/);
    # Use the first domain found.
    $domain = $tmp unless (length $domain);
  }

  $pathinfo =~ s#^/+##;
  $pathinfo =~ s#/+$##;
  if ($pathinfo =~ m#([\w/.-]+)#) {
    $pathinfo = $1;
  }
  else {
    $pathinfo = '';
  }
 
  # Multiple lists in the path allow confirmation tokens for multiple
  # lists to be viewed at once with the showtokens command.
  @mlists = split '/', $pathinfo;
}

# Clean up the URL by which this program was invoked.
$cgiurl = $cgi->script_name;
if ($domain) {
  $cgiurl .= "/domain=$domain";
}
if ($pathinfo) {
  $cgiurl .= "/$pathinfo";
}
$cgiurl = Mj::Format::uescape($cgiurl);

unless ($domain) {
  @domains = Majordomo::domains($::LISTDIR);
  ($domain) = grep { lc $_ eq lc $ENV{'HTTP_HOST'} } @domains;
  $domain = $domains[0] unless $domain;
}

# Untaint the domain name.
if ($domain =~ /([a-zA-Z0-9\.\-]+)/) {
  $domain = $1;
}
else {
  &surrender($cgi, qq("$domain" is an invalid domain.), $domain);
}

#----- Make the Majordomo object -----#
$mj = new Majordomo $::LISTDIR, $domain;
&surrender($cgi, $mj, $domain) unless (ref $mj);

#----- Generate the session information. -----#
for my $i ('HTTP_HOST', 'REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "X-$i: $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= "Date: " . scalar(localtime(time)) . "\n";

#----- Determine the address of the user. -----#
$falseuser = "y$ENV{'REMOTE_ADDR'}\@example.com";
# Convert colons to underscores in IPv6 addresses
$falseuser =~ s/:/_/g;
$user      = $cgi->param('user') || $falseuser;

$ofunc   = $cgi->param('func') || 'welcome';
$mode    = join ('-', $cgi->param('mode')) || '';
$mode    =~ /^([a-zA-Z=-]*)/; $mode = lc $1;
@lists   = $cgi->param('list');
if ((! @lists) and @mlists) {
  @lists = @mlists;
}
$lists[0] ||= '';
# Strip white space
for ($i = 0; $i <= $#lists; $i++) {
  $lists[$i] =~ s/\s+//g;
}

#----- Determine what service was requested -----#
# Extract "mode" from command name
if ($ofunc =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  if (length $mode) {
    $mode = lc $2 . "-$mode";
  }
  else {
    $mode = lc $2;
  }
}
else { 
  $ofunc =~ /(.*)/; $ofunc = $1;
  $func = $ofunc;
}

#----- Determine the administrative password -----#
$opass = $cgi->param('passw');
if (defined $opass and length $opass) {
  if ($opass =~ /(\S+)/) { 
    $passw = $1;
  }
  else {
    $passw = '';
  }
}
else {
  $opass = $passw = '';
}

#----- Connect to the server -----#
($ok, $mess) = $mj->connect('wwwadm', $sess, $user, $passw);
unless ($ok) {
  &surrender($cgi, $mess, $domain);
}

#----- Initialize the variable substitutions -----#
$cgidata = Mj::Format::cgidata($mj, { 'user' => ($user eq $falseuser) ?
                                      '' : $user, 
                                      'password' => $passw });

$tmp = '';
if (defined $func and length $func) {
  $tmp = $func;
  if (defined $mode and length $mode) {
    $tmp .= "-$mode";
  }
}

$subs = { $mj->standard_subs($lists[0]),
          'CGIDATA'   => $cgidata,
          'CGIURL'    => $cgiurl,
          'CHUNKSIZE' => Mj::Format::escape($cgi->param('chunksize') || ''),
          'CMDPASS'   => '',
          'COMMAND'   => Mj::Format::escape($tmp),
          'EXTRA'     => '',
          'HEREARGS'  => '',
          'USER'      => ($user eq $falseuser) ? '' : 
                         Mj::Format::escape($user),
          'VICTIMS'   => '',
        };

@extra = $cgi->param('extra');
if (defined $extra[0]) {
  $subs->{'EXTRA'} = [];
  for $tmp (@extra) {
    next unless (defined $tmp and length $tmp);
    push @{$subs->{'EXTRA'}}, Mj::Format::escape($tmp);
  }
}

@hereargs = $cgi->param('hereargs');
if (defined $hereargs[0]) {
  $subs->{'HEREARGS'} = [];
  for $tmp (@extra) {
    next unless (defined $tmp and length $tmp);
    push @{$subs->{'HEREARGS'}}, Mj::Format::escape($tmp);
  }
}

@victims = $cgi->param('victims');
if (defined $victims[0]) {
  $subs->{'VICTIMS'} = [];
  for $tmp (@extra) {
    next unless (defined $tmp and length $tmp);
    push @{$subs->{'VICTIMS'}}, Mj::Format::escape($tmp);
  }
}

#----- Remove the temporary password if the admin is signing out. -----#
if ($ofunc eq 'logout') {
  $mj->del_latchkey($passw) if ($mj->t_recognize($passw));
  $subs->{'CMDPASS'} = '';
  $subs->{'COMMAND'} = 'welcome';
  $cgidata = Mj::Format::cgidata($mj, 
               { 
                 'user' => ($user eq $falseuser) ? '' : $user,
                 'password' => '',
               });
  $subs->{'CGIDATA'} = $cgidata;
}
else {
  $passw = $mj->gen_latchkey($passw) 
    if ($passw and !$mj->t_recognize($passw));
  $subs->{'CMDPASS'} = Mj::Format::escape($passw);
  $cgidata = Mj::Format::cgidata($mj, 
               { 
                 'user' => ($user eq $falseuser) ? '' : $user,
                 'password' => $passw,
               });
  $subs->{'CGIDATA'} = $cgidata;
}

#----- Should the header and footer be displayed? -----#
$raw = 0;
if (($func eq 'tokeninfo' and $mode =~ /part/ 
     and $mode !~ /delete|edit|replace/) or 
    ($func eq 'archive' and $mode =~ /get/ and $mode =~ /part/))
{
  $raw = 1;
}

#----- Print the generic page header -----#
unless ($raw) {
  print $cgi->header('-expires' => '+50s');
  if ($lists[0] =~ /^(ALL|DEFAULT|GLOBAL)/) {
    $tmp = $mj->format_get_string('wwwadm', 'head_global', 'GLOBAL');
  }
  else {
    $tmp = $mj->format_get_string('wwwadm', 'head', $lists[0]);
  }
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
}

@extra = $cgi->param('extra');

if ($ofunc eq 'logout') {
  $tmp = $mj->format_get_string('wwwadm', 'logout', $lists[0]);
  $str = $mj->substitute_vars_format($tmp, $subs);
  &usage($mj, $subs, $str, '');
}

unless (length $lists[0]) {
  &login($mj, $subs, '');
}

#----- Validate the list names -----#
for ($i = 0 ; $i < scalar @lists ; $i++) {
  $lists[$i] =~ s/\s+//g;
  $str = $lists[$i];
  $str =~ s/:[^:]*$//;

  unless (Majordomo::legal_list_name($str)) {
    $tmp = $mj->format_error('invalid_list', 'GLOBAL', 'LIST' => $str);
    &login($mj, $subs, $tmp);
  }
}

($truelist, $sublist, $mess) = $mj->valid_list($lists[0], 1, 1);
unless (defined $truelist and length $truelist) {
  unless (defined $mess and length $mess) {
    $mess = $mj->format_error('unknown_list', 'GLOBAL', 'LIST' => $lists[0]);
  }
  &login($mj, $subs, $mess);
}

$debug = $mj->list_config_get(undef, undef, $truelist, 'MAIN', 'debug') || 0;
$::log->set_level($debug);

unless ($passw) {
  &login($mj, $subs, '');
}

$addr = new Mj::Addr($user);

if (! defined $addr) {
  ($ok, $mess) = (0, $mj->format_error('undefined_address', $lists[0]));
}
else {
  ($ok, $mess, $loc) = $addr->valid;
  unless ($ok) {
    $mess = $mj->format_error('invalid_address', $lists[0], 'ADDRESS' => $user, 
                              'ERROR' => $mess, 'LOCATION' => $loc);
  }
}

unless ($ok) {
  &login($mj, $subs, $mess);
}

if ($ofunc =~ /newfaq|newinfo|newintro/) {
  $func = "put";
}

unless ($mj->validate_passwd($addr, $passw, $truelist, 'ANY')) {
  $subs->{'CMDPASS'} = '';
  if ($mj->t_recognize($opass)) {
    &login($mj, $subs, '');
  }

  $tmp = $mj->format_error('invalid_password', $truelist, 'VICTIM' => $user);

  # Record password failures in the log.
  $mj->inform('GLOBAL', 'connect', $user, $user, 
              "(wwwadm request from $ENV{'REMOTE_ADDR'})",
              'wwwadm', 0, 0, 0, $tmp, $::log->elapsed);

  &login($mj, $subs, $tmp);
}

if ($ofunc eq 'newlist') {
  &newlist($mj, $subs, '')
}
elsif ($ofunc eq 'login') {
  &login($mj, $subs, '');
}
elsif ($ofunc eq 'welcome') {
  $tmp = $cgi->param('doc') || $cgi->param('extra') || '';
  &usage($mj, $subs, '', $tmp);
}
elsif ($ofunc eq 'command') {
  &command($mj, $subs, '');
}
elsif (! exists $commands{$func}) {
  &usage($mj, $subs, 
         $mj->format_error('invalid_command', $lists[0], 
                           'COMMAND' => $func),
         '');
}

#----- Password check -----#
if (! $passw) {
  &usage($mj, $subs, 
         $mj->format_error('no_password', 'GLOBAL', 'COMMAND' => $func), '');
}

#----- List check -----#
if ($commands{$func}->[0] and ! scalar @lists) {
  &usage($mj, $subs, 
         $mj->format_error('no_list', 'GLOBAL', 'COMMAND' => $func), '');
}

no strict 'refs';

$request = {
    'cgidata'   => $cgidata,
    'cgiurl'    => $cgiurl,
    'command'   => $func,
    'list'      => $truelist,
    'sublist'   => $sublist,
    'mode'      => $mode,
    'password'  => $passw, 
    'user'      => $addr,
    'victims'   => [ @victims ],
};

# Deal with result sets
if ($func eq 'who' or $func eq 'which') {
  $request->{'chunksize'} = $cgi->param('chunksize') || 0;
  $request->{'start'} = $cgi->param('start') || 1;
}

if (function_prop($func, 'iter')) {
  $request->{'command'} .= '_start';
}


#----- Process command arguments -----#
# Normally, the "extra" variable is used to hold command arguments.
# However, there are some cases in which this approach is inadequate.
#
# The first is with the configset command, in the case that more
# than one setting is changed at once.
#
# The second exception is for the subscribe-set command, which
# must have both addresses and settings available.
#
# The third exception is for the accept and reject commands,
# which must allow an explanation to be sent back to the person
# whom the command affects.
#
# The fourth exception is for the set command, which must include
# separate settings for each address affected.
#
# The fifth exception is for the report command, which must include
# separate action and time span values.
#
# The sixth exception is for the configshow command, which allows
# an array of variables to be specified.
#
# The seventh exception is for the who or which command, which must
# include a search pattern.
#
# The eighth exception is for the createlist command, which
# can include a group of list owners, a password, and possibly
# a new list name.
#
# The ninth exception is for the archive search form.
#
# The tenth exception is for the name of a file retrieved by
# the get command.
#
# In each of these cases, the basic command form requires that the
# "extra" variable still be handled if it was used.
if (@extra and $func ne 'showtokens') {
  $i = 0;
  for $req (@extra) {
    $i++;
    Mj::Parser::parse_args($request, $req, \@hereargs);

    # Deal with accept/reject reasons and other values from textareas.
    if ($func =~ /accept|reject/) {
      $request->{'xplanation'} ||= $cgi->param('xplanation') || '';
    }
    elsif ($func eq 'configset' and not $cgi->param('command')) {
      @values = $cgi->param($req);
      @values = split ("\r?\n", $values[0]) unless ($#values);
      $request->{'value'} = [@values];
    }
    elsif ($mode =~ /replace/) {
      @values = split "\r?\n", $cgi->param('contents');
      $request->{'contents'} = [@values];
    }

    $result = $mj->dispatch($request); 

    if ($raw and $i == 1) {
      if (! $result->[0] or 
          ($func eq 'archive' and ! $request->{'part'}))
      {
        # The command failed, or the archive command is being used
        # to display a whole message, so print the page header.
        print $cgi->header('-expires' => '+50s');
        if ($request->{'list'} =~ /^(ALL|DEFAULT|GLOBAL)/) {
          $tmp = $mj->format_get_string('wwwadm', 'head_global',
                                        'GLOBAL');
        }
        else {
          $tmp = $mj->format_get_string('wwwadm', 'head', $lists[0]);
        }
        $str = $mj->substitute_vars_format($tmp, $subs);
        print STDOUT "$str\n";
        $raw = 0;
      }
      elsif ($request->{'part'} =~ /[hH]$/) {
        print $cgi->header('-type' => 'text/plain',
                           '-expires' => '+50s');
      }
      else {
        if ($func eq 'tokeninfo' and ! $request->{'part'}) {
          $tmp = 'text/plain';
          $cs = "";
        }
        elsif ($func eq 'tokeninfo') {
          $tmp = $result->[2]->{$request->{'part'}}->{'type'} || 'text/plain';
          $cs  = $result->[2]->{$request->{'part'}}->{'charset'};
        }
        else {
          $tmp = $result->[1]->[2]->{$request->{'part'}}->{'type'} 
            || 'text/plain';
          $cs  = $result->[1]->[2]->{$request->{'part'}}->{'charset'};
        }

        print $cgi->header('-type' => $tmp, '-charset' => $cs,
                           '-expires' => '+50s');
      }
    }

    &usage($mj, $subs, "Unable to obtain result from Majordomo.", '') 
      unless $result;

    &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwadm', $request, $result);

    if (function_prop($func, 'iter')) {
      $request->{'command'} =~ s/done/start/;
    }
  }
}
elsif ($func eq 'set') {
  @extra = $cgi->param('victims');
  unless (scalar @extra) {
    &usage($mj, $subs, $mj->format_error('no_address', $list));
  }
  for $addr (@extra) {
    @values = $cgi->param($addr);
    $request->{'setting'} = join ",", @values;
    for $j (qw(ackdeny ackpost ackreject ackstall eliminatecc hideaddress
               hideall hidepost postblock prefix replyto selfcopy rewritefrom)) {
      unless (grep { $_ eq $j } @values) {
        $request->{'setting'} .= ",no$j";
      }
    }
    $request->{'victims'} = [ $addr ];
    $result = $mj->dispatch($request); 
    &usage($mj, $subs, "Unable to obtain result from Majordomo.", '') 
      unless $result;
    &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwadm', $request, $result);
  }
}
# The remaining exceptions, other than showtokens, require only one dispatch call.
elsif ($func ne 'showtokens') {
  if ($func eq 'accept' or $func eq 'reject') {
    @extra = $cgi->param('tokens');
    $request->{'tokens'} = [ @extra ];
    $request->{'xplanation'} = join ("\n", $cgi->param('xplanation')) || '';
  }
  elsif ($func eq 'subscribe' or $func eq 'unsubscribe' 
         or $func eq 'register' or $func eq 'unregister') 
  {
    @extra = $cgi->param('victims');
    unless (scalar @extra) {
      &usage($mj, $subs, $mj->format_error('no_address', $list));
    }
    if (scalar @extra == 1) {
      @extra = split "\r?\n", $extra[0];
    }
    $request->{'victims'} = [ @extra ];
    if ($func eq 'subscribe' and $mode =~ /set/) {
      @extra = $cgi->param('setting');
      $request->{'setting'} = join ',', @extra;
      for $j (qw(ackdeny ackpost ackreject ackstall eliminatecc hideaddress
                 hideall hidepost postblock prefix replyto selfcopy 
                 rewritefrom)) 
      {
        unless (grep { $_ eq $j } @extra) {
          $request->{'setting'} .= ",no$j";
        }
      }
    }
    elsif ($func eq 'subscribe' and scalar @extra == 1) {
      # The administrator may have specified a name to go with the address.
      $tmp = $cgi->param('name');
      if (defined $tmp and $tmp =~ /\S/) {
        $addr = new Mj::Addr($extra[0]);

        unless (defined $addr) {
          &login($mj, $subs, $mj->format_error('undefined_address', $lists[0]));
        }

        ($ok, $mess, $loc) = $addr->setcomment($tmp);
        unless ($ok) {
          $mess = $mj->format_error($mess, 'GLOBAL');
          &login($mj, $subs, 
                 $mj->format_error('invalid_comment', $lists[0], 
                                   'COMMENT' => $tmp, 'ERROR' => $mess,
                                   'LOCATION' => $loc));
        }
        $request->{'victims'} = [ $addr ];
      }
    }
  }
  elsif ($func eq 'report') { 
    $request->{'action'} = join ',', $cgi->param('action');
    $request->{'date'} = $cgi->param('date');
  }
  elsif ($func eq 'configshow') {
    $request->{'groups'} = [ $cgi->param('groups') ];
  }
  elsif ($func eq 'who' or $func eq 'which') {
    $request->{'regexp'} = $cgi->param('pattern');
  }
  elsif ($func eq 'put') {
    if ($ofunc =~ /^new(faq|info|intro)/i) {
      $request->{'file'} = "/$1";
      $request->{'xdesc'} = 'default';
    }
    elsif ($mode =~ /dir|delete/) {
      $tmp = $cgi->param('path');
      $tmp = '' unless (defined $tmp);
      $tmp .= $cgi->param('file');
      $request->{'file'} = $tmp;
      if ($mode =~ /dir/) {
        $request->{'xdesc'} = $cgi->param('description') || '(dir)';
      }
    }
    elsif ($ofunc =~ /^put/ and $cgi->param('args')) {
      Mj::Parser::parse_args($request, $cgi->param('args'), '');
    }
    $tmp = $cgi->param('contents');
    $tmp =~ s/\r?\n$//;
    @extra = split "\r?\n", $tmp;
    $request->{'contents'} = [@extra];
  }
  elsif ($func eq 'createlist') {
    $request->{'newlist'} = $cgi->param('newlist');
    if ($cgi->param('newname')) {
      $request->{'newpasswd'} = $cgi->param('newname');
    }
    elsif ($cgi->param('newpasswd')) {
      unless ($request->{'mode'} =~ /pass/) {
        if (length $request->{'mode'}) {
          $request->{'mode'} .= '-pass';
        }
        else {
          $request->{'mode'} = 'pass';
        }
      }
      $request->{'newpasswd'} = $cgi->param('newpasswd');
    }
    $tmp = $cgi->param('owners') || '';
    $tmp =~ s/\r?\n$//;
    if ($tmp !~ /\S/ and $user =~ /example.com/) {
      &newlist($mj, $subs, $mj->format_error('undefined_address', 'GLOBAL'))
        unless ($mode =~ /destroy|nocreate|regen|rename/);
    }
    @extra = split "\r?\n", $tmp;
    $request->{'owners'} = [@extra];
  }
  elsif ($func eq 'archive') {
    ($start, $end) = cgi_get_dates($cgi);
    if (length $start and length $end) {
      $request->{'args'} = "$start - $end";
    }
    else {
      $request->{'args'} = "$start$end";
    }

    $request->{'contents'} = [];
    $tmp = $cgi->param('author');
    if (defined $tmp and length $tmp) {
      push @{$request->{'contents'}}, "~a$tmp";
    }
    $tmp = $cgi->param('subject');
    if (defined $tmp and length $tmp) {
      push @{$request->{'contents'}}, "~s$tmp";
    }
    if ($mode =~ /get/) {
      $tmp = $victims[0];
      unless (defined $tmp and length $tmp) {
        $subs->{'EXTRA'} = Mj::Format::escape($request->{'args'});
        $subs->{'HEREARGS'} = $request->{'contents'};
        &usage($mj, $subs, $mj->format_error('undefined_address', 
                             $lists[0]), 'choose_email');
      }
    }
  }
  elsif ($func eq 'get') {
    $tmp = $cgi->param('path');
    $tmp = '' unless (defined $tmp);
    $tmp .= $cgi->param('file');
    $request->{'path'} = $tmp;
  }
    
  $result = $mj->dispatch($request); 
  &usage($mj, $subs, "Unable to obtain result from Majordomo.", '') 
    unless $result;
  &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwadm', $request, $result);
}

#----- Display supplementary information -----#
# There are several supplementary screens which this script displays. 
#   command    - Display a command menu
#   configshow - Allow easy access to collections of settings.
#   newlist    - Allow lists to be created, renamed, or destroyed.
#   index      - Display an index of files and directories.
#   lists      - Show details about a list, or a collection of lists.
#   showtokens - Display a list of pending requests
#   which      - Search all subscriber lists or (un)register addresses
#   who        - Search the subscriber list or (un)subscribe addresses

$tmp = $cgi->param('command') || '';
if ($cgi->param('brief') or $raw) {
  # do not display supplemental information.
}
elsif (($tmp eq 'on' and $func ne 'showtokens')
    or $commands{$func}->[1] eq 'command') {
  &command($mj, $subs, '');
}
elsif ($commands{$func}->[1] =~ /^welcome_(\S*)/) {
  $tmp = $1 || '';
  &usage($mj, $subs, '', $tmp);
}
elsif ($commands{$func}->[1] eq 'newlist') {
  &newlist($mj, $subs, '');
}
elsif ($commands{$func}->[1] eq 'showtokens') {
  # A separate head and foot are needed because this interface
  # can display tokens for any number of mailing lists.
  $tmp = $mj->format_get_string('wwwadm', 'showtokens_head', $lists[0]);
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  Mj::Parser::parse_args($request, $extra[0], '')
    if ($func eq 'showtokens' and $extra[0]);
  $i = 0;
  $request->{'command'} = 'showtokens';
  $request->{'mode'} = '';
  my (%seen);
  for $list (@lists, @mlists) {
    next if (exists $seen{$list});
    $request->{'list'} = $list;
    $result = $mj->dispatch($request);
    # Increase the token count if any tokens were found.
    if ($result->[0]) {
      $i += $#$result;
    }
    &{"Mj::Format::showtokens"}($mj, \*STDOUT, \*STDOUT, 'wwwadm', 
                                      $request, $result);
    $seen{lc $list}++;
  }

  $subs->{'COUNT'} = $i;
  $tmp = $mj->format_get_string('wwwadm', 'showtokens_foot', $lists[0]);
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  $tmp = $cgi->param('command') || '';
  if ($tmp eq 'on' and $func eq 'showtokens') {
    &command($mj, $subs, '');
  }
}
elsif ($commands{$func}->[1] ne $func) {
  if ($commands{$func}->[1] eq 'configshow') {
    $func = $request->{'command'} = 'configshow';
    $request->{'mode'} = 'categories';
    $request->{'list'} = $lists[0];
  }
  elsif ($commands{$func}->[1] eq 'lists') {
    $func = $request->{'command'} = 'lists';
    $request->{'mode'} = 'full-long';
    $request->{'list'} = 'GLOBAL';
    $request->{'regexp'} = $lists[0];
  }
  elsif ($commands{$func}->[1] eq 'who') {
    $func = 'who';
    $request->{'command'} = 'who_start';
    $request->{'mode'} = 'summary';
    $request->{'list'} = $lists[0];
    $request->{'chunksize'} = '';
    $request->{'start'} = $cgi->param('start') || 1;
    $request->{'regexp'} = $cgi->param('pattern');
    $request->{'victims'} = '';
  }
  elsif ($commands{$func}->[1] eq 'which') {
    $func = 'which';
    $request->{'command'} = 'which';
    $request->{'mode'} = '';
    $request->{'list'} = 'GLOBAL';
    $request->{'chunksize'} = $cgi->param('chunksize') || 10;
    $request->{'regexp'} = $cgi->param('pattern');
    $request->{'victims'} = '';
  }
  elsif ($commands{$func}->[1] eq 'index') {
    $func = 'index';
    $request->{'command'} = 'index';
    $request->{'mode'} = '';
    $request->{'victims'} = '';
    if (exists $request->{'file'}) {
      $tmp = $request->{'file'};
      delete $request->{'file'};
      $tmp =~ s#[^/]+/*$##;
      $request->{'path'} = $tmp;
    }
  }
  $result = $mj->dispatch($request); 
  &usage($mj, $subs, "Unable to obtain result from Majordomo.", '') 
    unless $result;
  &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwadm', $request, $result);
}

#----- Print the Footer -----#
unless ($raw and $result->[0]) {
  if ($request->{'list'} =~ /^(ALL|DEFAULT|GLOBAL)/) {
    $tmp = $mj->format_get_string('wwwadm', 'foot_global', 'GLOBAL');
  }
  else {
    $tmp = $mj->format_get_string('wwwadm', 'foot', $lists[0]);
  }
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
}

#----- Subroutines -----#

sub command {
  my ($mj, $subs, $message) = @_;
  my ($str, $tmp);

  $subs->{'ERROR'} = Mj::Format::escape($message);
  $::log->message(20, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwadm', 'command', 'GLOBAL');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  $tmp = $mj->format_get_string('wwwadm', 'foot', 'GLOBAL');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub initialize_log {
  $ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

  umask oct($::UMASK);

  # After connecting, the log level is determined by the "debug"
  # configuration setting.
  $::log = new Mj::Log;
  $::log->add
    (
     method      => 'handle',
     id          => 'wwwadm',
     handle      => \*STDERR,
     level       => 20,
     subsystem   => 'mail',
     log_entries => 1,
     log_exits   => 1,
     log_args    => 1,
    );

  $::log->in(20, undef, "info", 
    "Majordomo WWW for List Owners - " . scalar(localtime) .
     " from $ENV{'REMOTE_ADDR'}");

  $::log->startup_time();
}

sub login {
  my $mj = shift; 
  my $subs = shift;
  my $message = shift || '';
  my ($str, $tmp);

  $subs->{'ERROR'} = Mj::Format::escape($message);
  $subs->{'COMMAND'} = "welcome"
   if ($subs->{'COMMAND'} eq "login");

  if ($message) {
    $::log->message(20, "info", $message);
  }

  $tmp = $mj->format_get_string('wwwadm', 'login', $subs->{'LIST'});
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  $tmp = $mj->format_get_string('wwwadm', 'foot', $subs->{'LIST'});
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub newlist {
  my ($mj, $subs, $message) = @_;
  my ($str, $tmp);

  $subs->{'ERROR'} = Mj::Format::escape($message);
  $::log->message(20, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwadm', 'newlist', $subs->{'LIST'});
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  if ($subs->{'LIST'} =~ /^(ALL|DEFAULT|GLOBAL)/) {
    $tmp = $mj->format_get_string('wwwadm', 'foot_global', 'GLOBAL');
  }
  else {
    $tmp = $mj->format_get_string('wwwadm', 'foot', $subs->{'LIST'});
  }
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub usage {
  my ($mj, $subs, $message, $helpfile) = @_;
  my ($str, $tmp);

  $subs->{'ERROR'} = Mj::Format::escape($message);
  $::log->message(20, "info", $message) if $message;

  if (defined $helpfile and length $helpfile) {
    $tmp = $mj->format_get_string('wwwadm', "welcome_$helpfile",
                                  $subs->{'LIST'});
  }
  elsif ($subs->{'LIST'} =~ /^(ALL|DEFAULT|GLOBAL)/) {
    $tmp = $mj->format_get_string('wwwadm', 'welcome_global',
                                  $subs->{'LIST'});
  }
  else {
    $tmp = $mj->format_get_string('wwwadm', 'welcome', $subs->{'LIST'});
  }
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  if ($subs->{'LIST'} =~ /^(ALL|DEFAULT|GLOBAL)/) {
    $tmp = $mj->format_get_string('wwwadm', 'foot_global', 'GLOBAL');
  }
  else {
    $tmp = $mj->format_get_string('wwwadm', 'foot', $subs->{'LIST'});
  }
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub surrender {
  my ($cgi, $message, $domain) = @_;
  my $url = $cgi->script_name;

  if ($message) {
    $::log->message(20, "info", $message);
    $message = Mj::Format::escape($message);
  }

  $domain = Mj::Format::escape($domain);
  
  print $cgi->header(-expires => '+50s');
  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>list administration: error</title>
</head>
<body>
<h2 align="center">Majordomo Error</h2>

<pre>$message</pre>

<p>
Type a valid Majordomo domain name into the <b>Domain</b> box.  
Press the <b>Sign In</b> button to receive further instructions.
</p>

<form method="POST" action="$url">
  <input type="hidden" name="func" value="login">
  Domain: 
  <input name="domain" size="45" value="$domain">
  <input type="submit" value="Sign In">
</form>
</body></html>
EOM

  exit 0;
}

sub cgi_get_dates {
  my $cgi = shift;
  my ($cm, $cy, $ed, $em, $end, $ey, $sd, $sm, $start, $sy);

  $start = $end = '';
  return unless (defined $cgi);

  $cm = sprintf "%.2d", (localtime)[4];
  $cy = (localtime)[5] + 1900;

  # days and months are assumed to have leading zeroes.
  # years are assumed to have four digits.
  $sd = $cgi->param('start_day') || '';
  $sm = $cgi->param('start_month') || '';
  $sy = $cgi->param('start_year') || '';
  $ed = $cgi->param('end_day') || '';
  $em = $cgi->param('end_month') || '';
  $ey = $cgi->param('end_year') || '';
 
  if (length $sd) {
    $sm ||= $cm;
    $sy ||= $cy;
    $start = "$sy$sm$sd";
  } 
  elsif (length $sm) {
    $sy ||= $cy;
    $start = "$sy$sm";
  }
  elsif (length $sy) {
    $start = $sy;
  }

  if (length $ed) {
    $em ||= $cm;
    $ey ||= $cy;
    $end = "$ey$em$ed";
  } 
  elsif (length $em) {
    $ey ||= $cy;
    $end = "$ey$em";
  }
  elsif (length $ey) {
    $end = $ey;
  }
 
  ($start, $end);
} 

=head1 COPYRIGHT

Copyright (c) 2000, 2002, 2004 Jason Tibbitts for The Majordomo
Development Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#^L
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***


