#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::DEFDOM = 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_confirm.debug");
  }
}

=head1 NAME

mj_confirm - simple web-based token acceptor

=head1 SYNOPSIS

Place a link in cgi-bin and configure Majordomo with the proper URL.

=head1 DESCRIPTION

When issuing tokens, Majordomo can provide the user with a URL to
visit to accept the token easily.  This script sits at that URL and
receives tokens to accept or deny.  If no token is provided, a text
field is presented where a token can be typed.  If no action is
provided, a radio control will be presented.

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::Format;
use Mj::Log;

my ($data, @args, $action, $approvals, $cgi, $cmdline, $date, $domain,
    $error, $expire, $mess, $mj, $ok, $ptoken, $result, $request, 
    $requester, $sess, $site_name, $token, $time, $tprefix, $ttype, $type);

$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

umask oct($::UMASK);

# Set up the log
$::log = new Mj::Log;
$::log->add
  (
   method      => 'handle',
   id          => 'mjwc',
   handle      => \*STDERR,
   level       => 500,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

$::log->in(20, undef, "info", "Majordomo webconfirm client - ".scalar(localtime));
$::log->startup_time();

# Start up the page and parse the URL
$cgi = new CGI;
print $cgi->header;
$ptoken = $cgi->param('t');
$action = $cgi->param('a');
$domain = $cgi->param('d') || $::DEFDOM;
if ($domain =~ /^([-\@\w.]+)$/) {
  $domain = $1;
}


# Make the Majordomo object
$::mj = new Majordomo $::LISTDIR, $domain;
$mj = $::mj;
unless (ref $mj) {
  print <<EOM; 
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>Majordomo 2 Token Confirmation: Error</title>
</head>
<body>
<h2>Error: $mj</h2>
</body></html>
EOM
  exit 0;
}

# Generate the session information
for my $i (keys(%ENV)) {
  $sess .= "$i = $ENV{$i}\n";
}
$sess .= $cgi->dump('true');

($ok, $mess) = $mj->connect('webconfirm', $sess);
unless ($ok) {
  print <<EOM; 
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>Majordomo 2 Token Confirmation: Error</title>
</head>
<body>
<h2>Error: $mess</h2>
</body></html>
EOM
  exit 0;
}

# Extract some useful configuration variables
$site_name =
  $mj->global_config_get(undef, undef, "site_name");

# Parse the token and extract some useful data from it
$token = $mj->t_recognize($ptoken) || "";
$ok = 0;

$request = {
    'command'   => 'tokeninfo',
    'list'      => '',
    'mode'      => '',
    'password'  => '',
    'user'      => 'unknown@anonymous',
    'token'     => $token,
    'tokens'    => [$token]
};
if ($token) {
  ($ok, $data, $sess) = @{$mj->dispatch($request)};
}

# We head the page with an error if we have one
$error = "";
if ($ptoken && (!$token || !$ok)) {
  $error = "Invalid token \"$ptoken\"!";
  $token = "";
}

# Now build a page
if (!$token || !$action) {
  if ($error) {
    $error="<b><i>Error: $error</i></b><p>\n";
  }
  
  # If we have a legal token, we can show some information about it
  # and hide the entry box
  if ($ok) {
    $ttype   = "hidden";
    $time    = localtime($data->{'time'});
    $expire  = localtime($data->{'expire'});
    $tprefix =<<EOF;
<table>
<tr>
<td align=right><b>Token:</b></td>
<td><tt>$token</tt></td>
<tr>
<td align=right><b>Request:</b></td>
<td><tt>$data->{'cmdline'}</tt></td>
<tr>
<td align=right><b>Submitted:</b></td>
<td><i>$time</i></td>
<tr>
<td align=right><b>Expires:</b></td>
<td><i>$expire</i></td>
<tr>
<td align=right><b>By:</b></td>
<td><i>$data->{'user'}</i></td>
</table>

EOF
  }  
  else {
    $tprefix="<b>Confirmation token:</b>";
    $ttype="text";
  }
  
  print <<EOF;
<html>
<head>
<title>
Request Confirmation
</title>
</head>
<body>
$error
<h1>Majordomo Request Confirmation</h1>
<form method=post>
<input name=\"d\" type=\"hidden\" value=\"$domain\">
$tprefix <input name=\"t\" type=\"$ttype\" value=\"$token\"><p>
<table>
<tr>
<td><b>Action:</b><br></td>
<td><input type=\"radio\" value=\"info\" name=\"a\"> Get more info<br>
    <input type=\"radio\" value=\"accept\" name=\"a\"> Accept<br>
    <input type=\"radio\" value=\"reject\" name=\"a\"> Reject<br>
</td></tr></table>
<input type=\"submit\" value=\"Click here to process\">
</form>
</body>
</html>
EOF
  
}

# We have all the arge we need; accept or reject and display the
# results
else {

  # Start the page
  print <<EOF;
<html>
<head>
<title>Request Confirmation Results</title>
</head>
<body>
<h1>Majordomo Request Confirmation Results</h1>
EOF

  # Call the appropriate function
  if ($action eq "accept") {
    print '<pre>';
    $request->{'command'} = "accept";
    ($ok) = @{
      Mj::Format::accept($mj, \*STDOUT, \*STDOUT, 'html', $request,
			 $mj->dispatch($request))};
  }
  elsif ($action eq "reject") {
    print '<pre>';
    $request->{'command'} = "reject";
    ($ok) =
      Mj::Format::reject($mj, \*STDOUT, \*STDOUT, 'html', $request,
			 $mj->dispatch($request));
  }
  elsif ($action eq "info") {
    $time    = localtime($data->{'time'});
    $expire  = localtime($data->{'expire'});
    print <<EOF;
<table>
<tr>
<td align=right><b>Token:</b></td>
<td><tt>$token</tt></td>
<tr>
<td align=right><b>Type:</b></td>
<td><i>$data->{'type'}</i></td>
<tr>
<td aligh=right><b>Approvals required:</b></td>
<td><i>$data->{'approvals'}</i></td>
<tr>
<td align=right><b>Request:</b></td>
<td><tt>$data->{'cmdline'}</tt></td>
<tr>
<td align=right><b>Submitted:</b></td>
<td><i>$time</i></td>
<tr>
<td align=right><b>Expires:</b></td>
<td><i>$expire</i></td>
<tr>
<td align=right><b>By:</b></td>
<td><i>$data->{'user'}</i></td>
</table>
<hr>
<b>Session information:</b><p>
<pre>
EOF
    if ($sess) {
      $request->{'sessionid'} = $data->{'sessionid'};
      Mj::Format::sessioninfo($mj, \*STDOUT, \*STDOUT, 'html', $request, [1, '']);
    }
  }
  else {
    print "Undefined action!\n";
  }
  print <<EOF;
</pre>
</body>
</html>
EOF
  
}

sub escape {
  local $_ = shift;
  my %esc = ( '&'=>'amp', '"'=>'quot', '<'=>'lt', '>'=>'gt');
  s/([<>\"&])/\&$esc{$1};/mg; 
  s/([\x80-\xFF])/'&#'.unpack('C',$1).';'/eg;
  $_;
}

$::log->out;
exit 0;

=head1 COPYRIGHT

Copyright (c) 1997, 1998 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

his program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***

