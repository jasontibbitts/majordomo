#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_confirm.debug");
  }
}

=head1 NAME

mj_confirm - simple web-based token acceptor

=head1 SYNOPSIS

Place a link in cgi-bin and configure Majordomo with the proper URL.

=head1 DESCRIPTION

When issuing tokens, Majordomo can provide the user with a URL to
visit to accept the token easily.  This script sits at that URL and
receives tokens to accept or reject.  If no token or action is provided, 
instructions will be displayed. 

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::CommandProps qw(:function);
use Mj::Format;
use Mj::Log;

my (%commands, @contents, @domains, $action, $cgi, $cgidata, $cgiurl, 
    $domain, $func, $i, $mess, $mj, $mode, $ok, $part, $pathinfo, 
    $ptoken, $result, $request, $sess, $str, $subs, $tmp, $token, $user);

$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

umask oct($::UMASK);

# Set up the log
$::log = new Mj::Log;
$::log->add
  (
   method      => 'handle',
   id          => 'wwwconfirm',
   handle      => \*STDERR,
   level       => 500,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

$::log->in(20, undef, "info", "Majordomo wwwconfirm client - ".scalar(localtime));
$::log->startup_time();

#----- Which commands are supported ?  -----#
# First value:  Should the result be preformatted?
%commands = (  
               'accept'    => [1],
               'login'     => [0],
               'reject'    => [1],
               'tokeninfo' => [0],
               'welcome'   => [0],
            );

# Start up the page and parse the URL
$cgi = new CGI;

$pathinfo = $domain = '';
if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  while ($pathinfo =~ s#/domain=([a-zA-Z0-9.-]+)##) {
    $domain = $1;
  }

  $pathinfo = '';
}

unless ($domain) {
  $domain = $cgi->param('d') || '';
}

# Clean up the URL by which this program was invoked.
$cgiurl = $cgi->script_name;
if ($domain) {
  $cgiurl .= "/domain=$domain";
}
$cgiurl = Mj::Format::uescape($cgiurl);

unless ($domain) {
  @domains = Majordomo::domains($::LISTDIR);
  ($domain) = grep { lc $_ eq lc $ENV{'HTTP_HOST'} } @domains;
  $domain = $domains[0] unless $domain;
}

# Untaint the domain name.
if ($domain =~ /([a-zA-Z0-9\.\-]+)/) {
  $domain = $1;
}
else {
  &surrender($cgi, qq("$domain" is an invalid domain.));
}


#----- Make the Majordomo object -----#
$mj = new Majordomo $::LISTDIR, $domain;
&surrender($cgi, '', $mj) unless (ref $mj);

#----- Generate the session information. -----#
for $i ('REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "X-$i: $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= "Date: " . scalar(localtime(time)) . "\n";

#----- Connect to the server -----#
($ok, $mess) = $mj->connect('wwwconfirm', $sess);
&surrender($cgi, '', $mess) unless $ok; 

#----- Collect some useful substitution variables -----#
$user   = "z$ENV{'REMOTE_ADDR'}\@example.com";
$ptoken = $cgi->param('t');
$action = $cgi->param('a') || 'tokeninfo-nosession';
$part   = $cgi->param('p') || '0';

$cgidata = Mj::Format::uescape("user=$user&d=$domain");

$token = $mj->t_recognize($ptoken) || "";

$subs = { $mj->standard_subs('GLOBAL'),
          'CGIDATA'   => $cgidata,
          'CGIURL'    => $cgiurl,
          'CMDPASS'   => '',
          'COMMAND'   => $action,
          'TOKEN'     => $token,
          'USER'      => $user,
        };


#----- Determine what service was requested -----#
if ($ptoken and not $token) {
  &login($mj, $cgi, $subs, qq(The token "$ptoken" is not valid.));
}
elsif (! ($token or $action eq 'welcome')) {
  &login($mj, $cgi, $subs, '');
}

# Extract "mode" from command name
if ($action =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  $mode = $2;
}
else { 
  $action =~ /(.*)/; $action = $1;
  $func = $action;
  $mode = '';
}

unless (exists $commands{$func}) {
  &usage($mj, $cgi, $subs, "$action is an unsupported command.");
}

if ($func eq 'welcome') {
  &usage($mj, $cgi, $subs, '');
}
elsif ($func eq 'login') {
  &login($mj, $cgi, $subs, '');
}

$request = {
  'cgiurl'    => $cgiurl,
  'command'   => $func,
  'id'        => $token,
  'list'      => 'GLOBAL',
  'mode'      => $mode,
  'part'      => $part,
  'password'  => '',
  'tokens'    => [$token],
  'user'      => $user,
};

if (function_prop($func, 'iter')) {
  $request->{'command'} .= '_start';
}

if ($func =~ /accept|reject/) {
  $request->{'xplanation'} = $cgi->param('xplanation') || '';
}
elsif ($func eq 'tokeninfo' and $mode =~ /replace/) {
  @contents = split "\r?\n", $cgi->param('contents');
  $request->{'contents'} = [@contents];
}

no strict 'refs';
$result = $mj->dispatch($request); 
&usage($mj, $cgi, $subs, "Unable to obtain result from Majordomo.") 
  unless ($result);

if ($func eq 'tokeninfo' 
    and $request->{'mode'} eq 'part'
    and $result->[0] > 0) 
{
  # Issue the correct HTTP header for this content-type, then display
  # the headers or body part.
  if ($part =~ /[hH]$/) {
    print $cgi->header('-type' => 'text/plain',
                       '-expires' => '+50s');
  }
  else {
    if ($part eq '0') {
      $tmp = 'text/plain';
    }
    else {
      $tmp = $result->[2]->{$part}->{'type'} || 'text/plain';
    }
 
    print $cgi->header('-type' => $tmp,
                       '-expires' => '+50s');
  }
}
else {
  #----- Print the header -----#
  print $cgi->header(-expires => '+10s');
  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
}

  print "<pre>\n" if ($commands{$func}->[0]);
  &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwconfirm', $request, $result);
  print "</pre>\n" if ($commands{$func}->[0]);;

unless ($func eq 'tokeninfo' 
        and $request->{'mode'} eq 'part'
        and $result->[0] > 0) 
{
  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
}

$::log->out;
exit 0;

#----- Subroutines -----#
sub login {
  my ($mj, $cgi, $subs, $message) = @_;
  my ($str, $tmp);

  print $cgi->header();
  $subs->{'ERROR'} = $message;
  $subs->{'COMMAND'} = "tokeninfo"
   if ($subs->{'COMMAND'} eq "login");

  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  if ($message) {
    $str = $message . "  Request: $subs->{'COMMAND'}  List: $subs->{'LIST'}";
    $::log->message(50, "info", $message);
  }

  $tmp = $mj->format_get_string('wwwconfirm', 'login');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  exit 1;
}

sub usage {
  my ($mj, $cgi, $subs, $message) = @_;
  my ($str, $tmp);

  print $cgi->header();
  $subs->{'ERROR'} = $message;
  $::log->message(50, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  $tmp = $mj->format_get_string('wwwconfirm', 'welcome');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  exit 1;
}

sub surrender {
  my ($cgi, $subs, $message) = @_;
  $::log->message(50, "info", $message) if $message;
  print $cgi->header(-expires => '+10s');
  my $url = $cgi->script_name;

  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>Majordomo 2 Request Confirmation: Error</title>
</head>
<body>
<h2 align=center>Majordomo Error</h2>
$message
<form method="POST" action="$url">
  <input type="hidden" name="func" value="login">
  Enter the domain name into the box, then press the <b>Sign In</b> button.<br>
  <input name="domain" size="45">
  <input type="submit" value="Sign In">
</form>
EOM

  if (defined $subs and exists $subs->{'MJOWNER'}) {
    print <<EOM;
Please contact 
<a href="mailto:$subs->{'MJOWNER'}">$subs->{'MJOWNER'}</a>
 for assistance.
EOM
  }

print "</body></html>\n";

  exit 0;
}


=head1 COPYRIGHT

Copyright (c) 1997, 1998, 2001, 2002 Jason Tibbitts for The Majordomo
Development Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***

