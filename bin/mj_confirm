#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_confirm.debug");
  }
}

=head1 NAME

mj_confirm - simple web-based token acceptor

=head1 SYNOPSIS

Place a link in cgi-bin and configure Majordomo with the proper URL.

=head1 DESCRIPTION

When issuing tokens, Majordomo can provide the user with a URL to
visit to accept the token easily.  This script sits at that URL and
receives tokens to accept or reject.  If no token or action is provided, 
instructions will be displayed. 

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::CommandProps qw(:function);
use Mj::Format;
use Mj::Log;

my (%commands, @contents, @domains, $action, $cgi, $cgidata, $cgiurl, 
    $domain, $func, $i, $mess, $mj, $mode, $ok, $part, $pathinfo, 
    $ptoken, $result, $request, $sess, $str, $subs, $tmp, $token, $user);

$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

umask oct($::UMASK);

# Set up the log
$::log = new Mj::Log;
$::log->add
  (
   method      => 'handle',
   id          => 'wwwconfirm',
   handle      => \*STDERR,
   level       => 500,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

$::log->in(20, undef, "info", "Majordomo wwwconfirm client - ".scalar(localtime));
$::log->startup_time();

#----- Which commands are supported ?  -----#
# First value:  Should the result be preformatted?
%commands = (  
               'accept'    => 1,
               'login'     => 1,
               'reject'    => 1,
               'tokeninfo' => 1,
               'welcome'   => 1,
            );

#----- Initialize the CGI object -----#
$cgi = new CGI;

#----- Determine the domain name  -----#
$pathinfo = '';
$domain = $cgi->param('d') || $cgi->param('domain') || '';

if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  while ($pathinfo =~ s#/domain=([^/]+)##) {
    $tmp = $1;
    next if ($tmp =~ /[^a-zA-Z0-9.-]/);
    $domain = $tmp unless (length $domain);
  }

  $pathinfo = '';
}

# Clean up the URL by which this program was invoked.
$cgiurl = $cgi->script_name;
if ($domain) {
  $cgiurl .= "/domain=$domain";
}
$cgiurl = Mj::Format::uescape($cgiurl);

unless ($domain) {
  @domains = Majordomo::domains($::LISTDIR);
  ($domain) = grep { lc $_ eq lc $ENV{'HTTP_HOST'} } @domains;
  $domain = $domains[0] unless $domain;
}

# Untaint the domain name.
if ($domain =~ /([a-zA-Z0-9\.\-]+)/) {
  $domain = $1;
}
else {
  &surrender($cgi, qq("$domain" is an invalid domain.));
}

#----- Make the Majordomo object -----#
$mj = new Majordomo $::LISTDIR, $domain;
&surrender($cgi, $mj) unless (ref $mj);

#----- Generate the session information. -----#
for $i ('REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "X-$i: $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= "Date: " . scalar(localtime(time)) . "\n";

#----- Connect to the server -----#
($ok, $mess) = $mj->connect('wwwconfirm', $sess);
&surrender($cgi, $mess) unless $ok; 

#----- Collect some useful substitution variables -----#
$user   = "z$ENV{'REMOTE_ADDR'}\@example.com";
$ptoken = $cgi->param('t');
$action = $cgi->param('a') || 'tokeninfo-nosession';
$part   = $cgi->param('p') || '0';

$cgidata = Mj::Format::cgidata($mj, { 'user' => $user,
                                      'password' => '',
                                    });

$token = $mj->t_recognize($ptoken) || '';

$subs = { $mj->standard_subs('GLOBAL'),
          'CGIDATA'   => $cgidata,
          'CGIURL'    => $cgiurl,
          'CMDPASS'   => '',
          'COMMAND'   => Mj::Format::escape($action),
          'TOKEN'     => Mj::Format::escape($token),
          'USER'      => Mj::Format::escape($user),
        };

#----- Determine what service was requested -----#
if ($ptoken and not $token) {
  &login($mj, $cgi, $subs, 
         $mj->format_error('invalid_token', 'GLOBAL', 
                           'TOKEN' => $ptoken));
}
elsif (! ($token or $action eq 'welcome')) {
  &login($mj, $cgi, $subs, '');
}

# Extract "mode" from command name
if ($action =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  $mode = $2;
}
else { 
  $action =~ /(.*)/; $action = $1;
  $func = $action;
  $mode = '';
}

unless (exists $commands{$func}) {
  &usage($mj, $cgi, $subs, 
         qq(The "$action" command is not supported.));
}

if ($func eq 'welcome') {
  &usage($mj, $cgi, $subs, '');
}
elsif ($func eq 'login') {
  &login($mj, $cgi, $subs, '');
}

$request = {
  'cgidata'   => $cgidata,
  'cgiurl'    => $cgiurl,
  'command'   => $func,
  'id'        => $token,
  'list'      => 'GLOBAL',
  'mode'      => $mode,
  'part'      => $part,
  'password'  => '',
  'tokens'    => [$token],
  'user'      => $user,
};

if (function_prop($func, 'iter')) {
  $request->{'command'} .= '_start';
}

if ($func =~ /accept|reject/) {
  $request->{'xplanation'} = $cgi->param('xplanation') || '';
}
elsif ($func eq 'tokeninfo' and $mode =~ /replace/) {
  @contents = split "\r?\n", $cgi->param('contents');
  $request->{'contents'} = [@contents];
}

no strict 'refs';
$result = $mj->dispatch($request); 
&usage($mj, $cgi, $subs, "Unable to obtain result from Majordomo.") 
  unless ($result);

if ($func eq 'tokeninfo' 
    and $request->{'mode'} eq 'part'
    and $result->[0] > 0) 
{
  # Issue the correct HTTP header for this content-type, then display
  # the headers or body part.
  if ($part =~ /[hH]$/) {
    print $cgi->header('-type' => 'text/plain',
                       '-expires' => '+50s');
  }
  else {
    if ($part eq '0') {
      $tmp = 'text/plain';
    }
    else {
      $tmp = $result->[2]->{$part}->{'type'} || 'text/plain';
    }
 
    print $cgi->header('-type' => $tmp,
                       '-expires' => '+50s');
  }
}
else {
  #----- Print the header -----#
  print $cgi->header(-expires => '+10s');
  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
}

  &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwconfirm', 
                         $request, $result);

unless ($func eq 'tokeninfo' 
        and $request->{'mode'} eq 'part'
        and $result->[0] > 0) 
{
  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
}

$::log->out;
exit 0;

#----- Subroutines -----#
sub login {
  my ($mj, $cgi, $subs, $message) = @_;
  my ($str, $tmp);

  print $cgi->header();
  $subs->{'ERROR'} = Mj::Format::escape($message);
  $subs->{'COMMAND'} = "tokeninfo"
   if ($subs->{'COMMAND'} eq "login");

  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  if ($message) {
    $str = $message . "  Request: $subs->{'COMMAND'}  List: $subs->{'LIST'}";
    $::log->message(50, "info", $message);
  }

  $tmp = $mj->format_get_string('wwwconfirm', 'login');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  exit 1;
}

sub usage {
  my ($mj, $cgi, $subs, $message) = @_;
  my ($str, $tmp);

  print $cgi->header();
  $subs->{'ERROR'} = Mj::Format::escape($message);
  $::log->message(50, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwconfirm', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  $tmp = $mj->format_get_string('wwwconfirm', 'welcome');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";

  $tmp = $mj->format_get_string('wwwconfirm', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print "$str\n";
  
  exit 1;
}

sub surrender {
  my ($cgi, $message) = @_;
  my $url = $cgi->script_name;

  if ($message) {
    $::log->message(50, "info", $message);
    $message = Mj::Format::escape($message);
  }
  
  print $cgi->header(-expires => '+10s');

  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>Majordomo 2 Request Confirmation: Error</title>
</head>
<body>
<h2 align=center>Majordomo Error</h2>

<pre>$message</pre>

<p>
Type a valid Majordomo domain name into the <b>Domain</b> box.
Press the <b>Sign In</b> button to receive further instructions.
</p>

<form method="POST" action="$url">
  <input type="hidden" name="func" value="login">
  Domain: 
  <input name="domain" size="45">
  <input type="submit" value="Sign In">
</form>
</body></html>
EOM

  exit 0;
}


=head1 COPYRIGHT

Copyright (c) 1997, 1998, 2001, 2002 Jason Tibbitts for The Majordomo
Development Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***

