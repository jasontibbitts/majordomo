#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::DEFDOM = 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_wwwusr.debug");
  }
}

=head1 NAME

mj_wwwusr - demonstration of majordomo user interface .

=head1 SYNOPSIS

Extend this file to create specialized CGI scripts.

=head1 DESCRIPTION

When utilizing majordomo with a CGI script, there are
several basic steps that must be taken, including initializing
majordomo variables,  creating a log (STDERR by default), 
initializing a CGI object, and starting a majordomo session
with connect().

This script performs these basic steps, easing the 
creation of new CGI scripts.  Adapt it to suit your needs.

Note that it may be necessary to install a setuid wrapper
for any script based upon this example.

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::CommandProps qw(:command :function);
use Mj::Parser;
use Mj::Format;
use Mj::Log;
use Mj::Addr;

my ($cgi, $domain, $mj, $ok, $mess, $serveradmin, $sess, $goodpass, $isreg);
my ($ofunc, $func, $user, $passw, $pathinfo, $mode, $list, $vict, $extra);
my ($line, $site, $file, $fh, $cgiurl, $admurl, $cmdform, $serveraddr, $falseuser);
my ($request, $result);

$ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";

umask oct($::UMASK);

# Change the log level to a higher number (500) for complete debugging
$::log = new Mj::Log;
$::log->add
  (
   method      => 'handle',
   id          => 'mjusr',
   handle      => \*STDERR,
   level       => 50,
   subsystem   => 'mail',
   log_entries => 1,
   log_exits   => 1,
   log_args    => 1,
  );

$::log->in(20, undef, "info", "Majordomo 2 WWW for Subscribers - ".scalar(localtime)." from $ENV{'REMOTE_ADDR'}");
$::log->startup_time();

# Start up the page and parse the URL
$cgi = new CGI;
print $cgi->header;
$domain = $::DEFDOM;

# Make the Majordomo object
$mj = new Majordomo $::LISTDIR, $domain;
&surrender($mj) unless (ref $mj);

$cgiurl = $mj->{'sitedata'}{'config'}{'cgi_url'} . "mj_wwwusr";
$admurl = $mj->{'sitedata'}{'config'}{'cgi_url'} . "mj_wwwadm";
$serveradmin = $mj->_global_config_get('whoami_owner');
$pathinfo = '';

# Generate the session information
for my $i ('REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "$i = $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= scalar(localtime(time)) . "\n";


if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  $pathinfo =~ m#/([^/\s]+)#;
  $pathinfo = $1;
}

$site        = $mj->_global_config_get('site_name');
$serveraddr  = $mj->_global_config_get('whoami');
$falseuser   = "x$ENV{'REMOTE_ADDR'}\@example.com";
# GET poses a security risk due to passwords being visible
# Set HTML form variables
$ofunc   = $cgi->param('func') || "lists";
$user    = $cgi->param('user') || $pathinfo || $falseuser;
$user    = new Mj::Addr($user) if $user;
$isreg   = $mj->{'reg'}->lookup($user->canon) if $user;
$passw   = $cgi->param('passw') || '';
$list    = $cgi->param('list') || 'GLOBAL';
$extra   = $cgi->param('extra') || '';

($ok, $mess) = $mj->connect('mj_wwwusr', $sess, $user);
unless ($ok) {
  $mess .= "Please contact <a href=\"mailto:$serveradmin\">$serveradmin</a> for assistance\n";
  &surrender($mess);
} 

# Extract "mode" from command name
if ($ofunc =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  $mode = $2;
}
else { 
  $ofunc =~ /(.*)/; $ofunc = $1;
  $func = $ofunc;
  $mode = '';
}

&print_header($user, $passw, $site, $cgiurl);

# Print help information if it was requested
if ($func =~ /about/) {
  &usage($mj);
  exit 0;
}

no strict 'refs';
##
## Validation section: address, command, list, password 
##
($ok, $mess) = $user->valid;
unless ($ok) {
  $::log->message(50, "info",  "Invalid address $user in form data.");
  &surrender("$user is an invalid address: $mess\n");
}

# Make sure the command is valid
if (not $func = command_legal($func)) {
  $::log->message(50, "info",  "Invalid command $func in form data.");
  &surrender("Illegal command: $func\n");
}
if ($func =~ /subscribe/ and $user->strip =~ /example.com$/i) {
  print "<h2>Please do not (un)subscribe example.com addresses.</h2>\n";
  &usage($mj);
}
if ($func =~ /subscribe|set/ and $list eq 'GLOBAL') {
  print "<h2>Please select a mailing list before pressing the (un)subscribe or set button.</h2>\n";
  &usage($mj);
}

# Make sure the list name is valid
if ($list) {
  unless ($list = $mj->valid_list($list, 0, 1)) {
    print "<h3>Invalid list name: $list</h3>\n";
    $::log->message(50, "info",  "Invalid list $list in form data.");
    &surrender("Invalid list name: $list\n");
  }
}

if ($user and $passw) {
  $passw =~ /(.+)/; $passw = $1;
  $goodpass = 
    $mj->validate_passwd($user, $passw, '', 'mj_wwwusr', 'GLOBAL', 'show'); 
}
##
## Construct the request
##
$request = {
  'auth'      => '',
  'command'   => $func,
  'interface' => 'mjusr',
  'list'      => $list,
  'mode'      => $mode,
  'password'  => $passw,
  'user'      => $user,
};
Mj::Parser::parse_args ($request, $extra);
if (function_prop($func, 'iter')) {
  $request->{'command'} .= '_start';
}

if ($func and $func =~ /info|intro|faq|subscribe/) {
  $result = $mj->dispatch($request); 
  &surrender("Unable to obtain result from Majordomo.\n") unless $result;

  $file = Majordomo::tempname();
  $fh = new IO::File ">$file";
  &surrender("Unable to create temporary file.\n") unless defined $fh;

  &{"Mj::Format::$func"}($mj, $fh, $fh, 'html', $request, $result);
  $fh->close();
  &format_print($file);
  unlink $file;
}
elsif ($func eq 'set' and $goodpass) {
  my ($i, $j, @settings);
  my @params = $cgi->param;
  for $j (qw(delivery ack eliminatecc hide prefix replyto selfcopy rewritefrom)) {
    $i = "$list:$j";
    if ($j eq 'delivery') {
      push @settings, $cgi->param($i);
    }
    elsif ($j eq 'hide' or $j eq 'ack') {
      $cgi->param($i) eq 'on' ? 
        push @settings, "${j}all" :
        push @settings, "no$j";
    } 
    else {
      $cgi->param($i) eq 'on' ? 
        push @settings, $j :
        push @settings, "no$j";
    }
  }
  $request->{'setting'} = join ",", @settings;
  ($ok, $mess) = @{$mj->dispatch($request)};
  print "<pre>Error for \"$i\" setting:  $mess</pre>\n" unless ($ok > 0);
  print "<pre>Your \"set\" command was completed.</pre>\n";
}


# There are three screens which this script displays.  The
# first is a list of all lists that are visible to the 
# subscriber.  The second contains information about the
# subscriber's account.  The third offers help.

if ($user and $isreg and $goodpass) {
  if ($func !~ /^(lists|faq|info|intro|subscribe)/) {  
    $request->{'command'} = "show";
    &showdata($mj, $request);

    print  <<EOM;
</form>
<hr>
For assistance, please contact 
<a href="mailto:$serveradmin">$serveradmin</a>.
</body></html>
EOM

    exit 0;
  }
}

$request->{'command'} = "lists";
&showlists($mj, $request);

print <<EOM;
<hr>
For assistance, please contact 
<a href="mailto:$serveradmin">$serveradmin</a>.
</body></html>
EOM

exit 0;

#---------------- Subroutines -----------------#
sub showlists {
  my $mj = shift;
  my $request = shift;
  my ($list, $cat, $desc, $flags);
  my ($ok, $mess, @listdata) = 
    @{$mj->dispatch($request)};
  # Iterate over lists and add form buttons
  select STDOUT;
  if (! $ok) {
    print <<EOM;
<h2>Error: $mess</h2>
If you received an "Invalid password" error, here are 
the possible causes:
<ul>
<li>The address in the <b>address</b> box is not registered at this site.
<li>The password in the <b>password</b> box is incorrect.
</ul>
A password is necessary if you wish to use the <b>show, set,</b>
or <b>unsubscribe</b> command.
<p>
A password is probably not necessary if you wish to use the <b>subscribe,
faq, info, intro, lists</b>, or <b>about this service</b> command.
EOM

    return;
  }
  print <<EOM;
<input type=submit name=func value=subscribe>
<input type=submit name=func value=faq>
<input type=submit name=func value=info>
<input type=submit name=func value=intro><br>
<b>Select a mailing list, then press one of the buttons above.
Leave the password box empty unless you have already been
assigned a password by majordomo at this site.</b>
<pre>
EOM
  for (@listdata) {
    ($list, $cat, $desc, $flags) =  @$_;
    $desc ||= "(no description)";
    print "<input type=radio name=list value=$list>";
    printf "%-24s %-12s %s\n", $list, $cat, $desc;
  }
  print "</pre></form>\n";
}

sub showdata {
  my $mj = shift;
  my $request = shift;
  my ($i, $j, @lists, $lfull, $class, $subtime); 
  my ($lflags, $lchangetime, $changetime);
  my ($ok, $listdata) =
    @{$mj->dispatch($request)};
  # Present the data, including form buttons.
  if ($ok < 1) {
    print "<h2>Error: $listdata->{'error'}</h2>\n";
    return $ok;
  }
  if (@{$listdata->{'aliases'}}) {
    print "<br>Aliases:<ul>\n";
    for $i (@{$listdata->{'aliases'}}) {
      next if $i eq $listdata->{'strip'};
      print "<li>$i\n";
    }
    print "</ul>\n";
  }
  @lists = sort keys %{$listdata->{'lists'}};
  unless (@lists) {
    print "<p>$user is not subscribed to any lists.\n";
    return 1;
  }

  print <<EOM;
<input type=submit name=func value="set">
<input type=submit name=func value="unsubscribe">
<p>
To unsubscribe, press the radio button for the 
appropriate list, then press the <b>unsubscribe</b> button above.
<br>
To change settings, press the radio button for a
list, choose the settings you want, then press the <b>set</b> button
above.  The settings are described at the bottom of this page.
<p>
EOM

  print "<table border><th>Mailing List<th>Delivery Mode<th>Ack<th>EliminateCC\n";
  print "<th>Hide<th>Prefix<th>ReplyTo<th>SelfCopy<th>RewriteFrom\n";
  for $i (@lists) {
    $lfull      = $listdata->{'lists'}{$i}->{'fulladdr'};
    $class      = $listdata->{'lists'}{$i}->{'class'};
    $subtime    = $listdata->{'lists'}{$i}->{'subtime'};
    $changetime = $listdata->{'lists'}{$i}->{'changetime'};
    $lflags     = $listdata->{'lists'}{$i}->{'flags'};
    print <<EOM;
    <tr><td align=center><input type=radio name=list value=$i>$i<br>
    <td align=center><select name="$i:delivery">
EOM
    for $j (qw (each digest nomail)) {
      if ($class eq $j) {
        print "<option selected> $j\n"; 
      }
      else {
        print "<option> $j\n";
      }
    }
    print "</select>\n";
    for $j (@{$lflags}) {
      $j =~ /(no)?(.+)/;  print $1 ?
          "<td align=center><input type=checkbox name=\"$i:$2\">\n"
        : "<td align=center><input type=checkbox name=\"$i:$2\" checked>\n";
    }
  }
  print "</table>\n";
  print "$user was registered on ".gmtime($listdata->{'regdata'}->{'regtime'})." UTC.<br>\n";

  print <<EOM;
<h2 align=center>What do the settings mean?</h2>
<ul>
<li>Delivery Mode: "each" means that you will receive each
message.  "digest" means that messages will be bundled into
a collection called a digest, which is sent periodically.
"nomail" means that you will receive no mail from the list.
This is a useful setting if you wish to postpone mail 
while you are traveling, for example.
<li>Ack:  Select this if you want to receive acknowledgements if your
posted messages need to be reviewed by the list owners
before they appear on a mailing list.
<li>EliminateCC: Select this if you do not want two copies
when someone sends a message both to you and to the list.
<li>Hide:  Select this if you do not want your address to appear
when someone reviews the list of subscribers.
<li>Prefix:  Select this if you want to see a prefix which indicates
the name of the mailing list in the Subject header of every message.
This only matters if the list owners have configured the list to
create a prefix.
<li>ReplyTo:  Select this if you want your replies to go to the
mailing list instead of to the person who wrote the original message.
This only matters if the list owners have configured the list to 
allow replies to go to the list.
<li>SelfCopy:  Select this if you want to receive a copy of messages
that you post to the mailing list.
<li>RewriteFrom:  Select this if you want to change the From header 
in messages you send to the list.  The From header will then be
rewritten to appear as it did when you subscribed to the list.
</ul>
EOM
}
  
sub format_print {
  my $file = shift;
  my ($line, $summarize, $hdrs);
  $summarize = $hdrs = 0;
  select STDOUT;

  $fh = new IO::File "<$file";
  if (! $fh) { 
    print  "<h3>Unable to get results</h3>\n";
    exit 0;
  }
  print  "<pre>\n";
  while ($line = $fh->getline()) { print $line; }
  $fh->close();
  print "</pre>\n";
}

sub usage {
  my ($mj) = shift;
  my $site  = $mj->_global_config_get('site_name');
  my $serveraddr = $mj->_global_config_get('whoami');
  my $serveradmin = $mj->_global_config_get('whoami_owner');

  print <<EOM;
<p>
This is the help page for the Majordomo 2 subscriber interface.
<p>
This interface serves two main purposes.  The first is to 
allow the public to learn about and subscribe to the mailing 
lists at $site.  The second is to allow subscribers to change
the settings for their subscriptions and to remove themselves
from mailing lists.
<p>
Three buttons are always present:
<ul>
<li><b>about this service</b> shows this help page.
<li><b>lists</b> shows all public mailing lists at this site.  
<br>Four buttons will be added when viewing this page:
<ul>
<li><b>subscribe</b> causes your address to be added
to the mailing list you have chosen.  Make sure
that your e-mail address appears in the "address:" box.
Unless you have already subscribed to at least one
list at this site, pressing this button will cause
a confirmation e-mail message to be sent to your address.
Follow the instructions in that message to complete
your subscription.
<li><b>faq</b> displays a list of frequently asked questions
and answers for a mailing list, if one exists.
<li><b>info</b> displays the "welcome message" that all
new subscribers receive.  It usually describes the policies
of the list owners.
<li><b>intro</b> displays an introduction to the topics
which are discussed on the mailing list, if one exists.
</ul>

Each mailing list has a radio button to the left
of its name.  It is important that you choose a mailing
list before pressing the <b>subscribe, faq, info</b> or
<b>intro</b> buttons.
<li><b>show</b> displays information about your account
if you have already subscribed and if you have typed your
password into the "password:" box.  If you press the 
<b>show</b> button without using your password, you 
will see the list of mailing lists instead.
<p>
If you want to access your account, add a solidus ("/")
and your e-mail address to this URL, for example: <br>
 <a href="$cgiurl/$user">$cgiurl/$user</a><br>
and bookmark that page for future reference.
When the list of lists appears, type in your password and
press the <b>show</b> button.  Your password appears in the 
welcome message you receive every time you subscribe to 
a mailing list, so be sure to save those messages.
<p>
It is possible for you to subscribe to mailing lists without
using a password.  However, using your password may
save you an extra step.  Confirmation instructions
will be sent via e-mail to your address if you do not
use your password.
<p>
Instructions for changing your account appear at the bottom of the 
screen after you press the <b>show</b> button.
</ul>
<hr>
If you have questions about this service, please write to
the site administrator at
<a href="mailto:$serveradmin">$serveradmin</a>.
EOM

  exit 0;
}

sub surrender {
  my ($message) = shift;
  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd"> 
<html><head>
<title>Majordomo 2 WWW for Subscribers: Error</title>
</head>
<body>
<h2>Error</h2>
$message;
</body></html>
EOM
  exit 0;
}

# XXX Move this to a template file.
sub print_header {
  my ($user, $passw, $site, $cgiurl) = @_;
  $user = Mj::Format::escape($user);
  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd"> 
<html><head>
<title>Majordomo 2 WWW for Subscribers</title>
</head>
<body>
<h2 align=center>List Information and Account Maintenance</h2>
Welcome to $site!  Here you will find information about our
mailing lists and your account on our Majordomo 2 list server.
<ul>
<li>Press <b>about this service</b> to see a detailed
help page.
<li>Press <b>lists</b> to see a list of mailing lists
at $site.
<li>Press <b>show</b> to see details about your account.
(password required)
</ul>
EOM

print "<h4 align=center>Type your address into the first box</h4>\n"
  if ($user =~ /example.com/);

  print <<EOM;
<form method=POST action=$cgiurl>
address:
<input size=34 name=user value="$user">
password:
<input size=10 name=passw type=password value="$passw"><br>
<input type=submit name=func value="about this service">
<input type=submit name=func value="lists">
<input type=submit name=func value="show">
EOM
}

=head1 COPYRIGHT

Copyright (c) 2000 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#^L
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***


