#!/usr/local/bin/perl-latest -wT
BEGIN {
  $::LIBDIR = 'unset';
  $::LISTDIR= 'unset';
  $::TMPDIR = 'unset';
  $::LOCKDIR= "blah";
  $::UMASK  = "007";
  $SIG{__WARN__} = sub {print STDERR "--== $_[0]"};

  # Redirect standard error output.
  if (! -t STDERR) {
    open (STDERR, ">>$::TMPDIR/mj_wwwusr.debug");
  }
}

=head1 NAME

mj_wwwusr - demonstration of majordomo user interface .

=head1 SYNOPSIS

Extend this file to create specialized CGI scripts.

=head1 DESCRIPTION

When utilizing majordomo with a CGI script, there are
several basic steps that must be taken, including initializing
majordomo variables,  creating a log (STDERR by default), 
initializing a CGI object, and starting a majordomo session
with connect().

This script performs these basic steps, easing the 
creation of new CGI scripts.  Adapt it to suit your needs.

Note that it may be necessary to install a setuid wrapper
for any script based upon this example.

=cut
use lib "$::LIBDIR";
use strict;
use CGI;
use Majordomo;
use Mj::CommandProps qw(:function);
use Mj::Format;
use Mj::Log;
use Mj::Parser;

my (%commands, @domains, @lists, $addr, $cgi, $cgidata, $cgiurl, $domain, $extra, 
    $falseuser, $func, $list, $mj, $mess, $mode, $ofunc, $ok, $passw, 
    $pathinfo, $request, $result, $sess, $str, $subs, $tmp, $user);


#----- Which commands are supported  -----#
# First  value:  Is a password required?
# Second value:  Is a valid address required?
# Third  value:  Is a list required?
# Fourth value:  Should the result be preformatted?
# Fifth value:  Page to display after the result.
%commands = (   
                'alias'         => [1, 1, 0, 1, 'show'],
                'changeaddr'    => [1, 1, 0, 1, 'show'],
                'faq'           => [0, 0, 1, 0, 'list'],
                'help'          => [0, 0, 0, 1, 'help'],
                'info'          => [0, 0, 1, 0, 'list'],
                'intro'         => [0, 0, 1, 0, 'list'],
                'lists'         => [0, 0, 0, 0, 'lists'],
                'password'      => [0, 1, 0, 1, 'login'],
                'register'      => [0, 1, 0, 1, 'welcome'],
                'set'           => [1, 1, 1, 1, 'show'],
                'show'          => [1, 1, 0, 0, 'show'],
                'subscribe'     => [0, 1, 1, 1, 'list'],
                'unregister'    => [1, 1, 0, 1, 'welcome'],
                'unsubscribe'   => [1, 1, 1, 1, 'show'],
            );
             
#----- Initialize the Log -----#
&initialize_log;

#----- Initialize the CGI object -----#
$cgi = new CGI;

$tmp = '+10s';
print $cgi->header(-expires => $tmp);

$pathinfo = $domain = '';
if (exists $ENV{'PATH_INFO'}) {
  $pathinfo = $ENV{'PATH_INFO'};
  while ($pathinfo =~ s#/domain=([a-zA-Z0-9.-]+)##) {
    $domain = $1;
  }

  $pathinfo =~ s#^/+##;
  $pathinfo =~ s#/+$##;
  if ($pathinfo =~ m#(.+\@[^/]+)#) {
    $pathinfo = $1;
  }
  else {
    $pathinfo = '';
  }
}

unless ($domain) {
  $domain = $cgi->param('domain') || '';
}

# Clean up the URL by which this program was invoked.
$cgiurl = $cgi->script_name;
if ($domain) {
  $cgiurl .= "/domain=$domain";
}
if ($pathinfo) {
  $cgiurl .= "/$pathinfo";
}
$cgiurl = Mj::Format::uescape($cgiurl);

unless ($domain) {
  @domains = Majordomo::domains($::LISTDIR);
  ($domain) = grep { $_ eq $ENV{'HTTP_HOST'} } @domains;
  $domain = $domains[0] unless $domain;
}

# Untaint the domain name.
if ($domain =~ /([a-zA-Z0-9\.\-]+)/) {
  $domain = $1;
}
else {
  &surrender(qq("$domain" is an invalid domain.));
}

#----- Make the Majordomo object -----#
$mj = new Majordomo $::LISTDIR, $domain;
&surrender($mj) unless (ref $mj);

#----- Generate the session information. -----#
for my $i ('REMOTE_ADDR','REMOTE_PORT', 'PATH_INFO') {
  $sess .= "X-$i: $ENV{$i}\n" if defined $ENV{$i};
}
$sess .= "Date: " . scalar(localtime(time)) . "\n";

#----- Determine the address of the user. -----#
$falseuser   = "x$ENV{'REMOTE_ADDR'}\@example.com";
$user    = $cgi->param('user') || $pathinfo || $falseuser;

#----- Connect to the server -----#
($ok, $mess) = $mj->connect('wwwusr', $sess, $user);

unless ($ok) {
  $subs = { $mj->standard_subs('GLOBAL'),
            'CGIDATA'  => '',
            'CGIURL'   => $cgi->script_name,
            'COMMAND'  => 'welcome',
            'DOMAIN'   => $domain,
            'PASSWORD' => '',
            'USER'     => ($falseuser eq $user) ? '':
                          Mj::Format::escape($user),
        };

  $tmp = $mj->format_get_string('wwwusr', 'head');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  &login($mj, $subs, $mess);
}

#----- Create a temporary password -----#
$passw   = $cgi->param('passw') || '';
if ($passw) {
  $passw =~ /(.+)/; $passw = $1;
  $passw = $mj->gen_latchkey($passw) 
    unless ($mj->t_recognize($passw));
}

$ofunc   = $cgi->param('func') || 'welcome';

$cgidata = sprintf 'passw=%s&user=%s', $passw, $user;
$cgidata = Mj::Format::uescape($cgidata);

$subs = { $mj->standard_subs('GLOBAL'),
          'CGIDATA'  => $cgidata,
          'CGIURL'   => $cgiurl,
          'COMMAND'  => $ofunc,
          'DOMAIN'   => $domain,
          'PASSWORD' => $passw,
          'USER'     => ($falseuser eq $user) ? '':
                        Mj::Format::escape($user),
        };

#----- Print the generic page header -----#
$tmp = $mj->format_get_string('wwwusr', 'head');
$str = $mj->substitute_vars_format($tmp, $subs);
print STDOUT "$str\n";

#----- Determine what service was requested -----#
if ($ofunc eq 'welcome') {
  &usage($mj, $subs, '');
}
elsif ($ofunc eq 'logout') {
  $mj->del_latchkey($passw);
  $subs->{'PASSWORD'} = '';
  $subs->{'COMMAND'} = 'welcome';
  &usage($mj, $subs, 'You have successfully signed out.');
}
elsif ($ofunc eq 'login') {
  $subs->{'COMMAND'} = 'welcome';
  &login($mj, $subs, '');
}

# Extract "mode" from command name
if ($ofunc =~ /([^\s=-]+)[=-](.*)/) {
  $func = $1;
  $mode = $2;
}
else { 
  $ofunc =~ /(.*)/; $ofunc = $1;
  $func = $ofunc;
  $mode = '';
}

# Make sure the command is valid
unless (exists $commands{$func}) {
  &usage($mj, $subs, "$func is an unsupported command.");
}

#----- Password check -----#
if (! $passw) {
  &login($mj, $subs, "A password is required to use the $func command.")
    if ($commands{$func}->[0]);
}
else {
  $addr = new Mj::Addr($user);
  unless ($mj->validate_passwd($addr, $passw, 'GLOBAL', 'show')) {
    $subs->{'PASSWORD'} = '';
    &login($mj, $subs, 'Invalid password.');
  }
}

#----- Address check -----#
if ($commands{$func}->[1] and (!$user or $user =~ /example\.com$/i)) {
  &login($mj, $subs, "The address \"$user\" is invalid.");
}

#----- List check -----#
@lists   = $cgi->param('list');
if ($commands{$func}->[2] and ! scalar @lists) {
  &usage($mj, $subs, 
         "A mailing list must be provided to use the \"$func\" command.");
}
$lists[0] ||= 'GLOBAL';
  

#----- Construct the request -----#
$request = {
  'cgiurl'    => $cgiurl,
  'command'   => $func,
  'list'      => $lists[0],
  'mode'      => $mode,
  'password'  => $passw,
  'user'      => $user,
};
if (function_prop($func, 'iter')) {
  $request->{'command'} .= '_start';
}

$extra   = $cgi->param('extra') || '';

#----- For changeaddr to work, the user and victim must be exchanged. -----#
if ($func eq 'changeaddr') {
  $request->{'user'} = $extra;
  $extra = $user;
}
  
no strict 'refs';
#----- Convert the arguments into the appropriate request values -----#
Mj::Parser::parse_args ($request, $extra) if ($extra);


for $list (@lists) {
  $request->{'list'} = $list;
  # For the 'set' command, convert the CGI data into the appropriate settings.
  if ($func eq 'set') {
    my ($i, $j, @params, @settings);
    @params = $cgi->param;
    for $j (qw(delivery ackdeny ackpost ackreject ackstall eliminatecc
               hideaddress hideall hidepost prefix replyto selfcopy 
               rewritefrom)) {
      $i = "$list;$j";
      if ($j eq 'delivery') {
        push @settings, $cgi->param($i);
      }
      else {
        if (! $cgi->param($i)) {
          push @settings, "no$j";
        }
        elsif ($cgi->param($i) eq 'on') {
          push @settings, $j;
        }
        # If the setting is disabled, ignore it.
      }
    }
    $request->{'setting'} = join ",", @settings;
  }
  elsif ($func eq 'password') {
    $tmp = $cgi->param('newpasswd');
    $tmp =~ s/\s+//g;
    if (defined $tmp and length $tmp) {
      Mj::Parser::parse_args ($request, $tmp, '');
    }
    else {
      $request->{'mode'} = 'gen';
    }
    $subs->{'PASSWORD'} = '';
    $subs->{'COMMAND'} = 'show';
  }

  $result = $mj->dispatch($request); 
  &usage($mj, $subs, "Unable to obtain result from Majordomo.") unless $result;
  print "<pre>\n" if ($commands{$func}->[3]);
  &{"Mj::Format::$func"}($mj, \*STDOUT, \*STDOUT, 'wwwusr', $request, $result);
  print "</pre>\n" if ($commands{$func}->[3]);;
}

#----- Display supplementary information -----#
# There are five supplementary screens which this script displays. 
#   list - Information about a single mailing list
#   lists - Information about all public mailing lists
#   login - Prompt for an e-mail address and password
#   show - Personal information about a subscriber
#   welcome - A help file that describes how to use the interface.

if ($commands{$func}->[4] ne $func) {
  if ($func eq 'changeaddr') {
    # restore original user if changeaddr did not succeed.
    $request->{'user'} = $user if ($result->[0] < 1);
    undef $request->{'victim'};
    undef $request->{'victims'};
  }
  if ($commands{$func}->[4] eq 'list') {
    $request->{'command'} = 'lists';
    $request->{'mode'} = 'full-long';
    $request->{'list'} = 'GLOBAL';
    for $list (@lists) {
      $request->{'regexp'} = $list;
      $result = $mj->dispatch($request); 
      &usage($mj, $subs, "Unable to obtain result from Majordomo.") 
        unless $result;
      &Mj::Format::lists($mj, \*STDOUT, \*STDOUT, 'wwwusr', $request, $result);
    }
  }
  elsif ($commands{$func}->[4] eq 'lists') {
    $request->{'command'} = 'lists';
    $request->{'mode'} = '';
    $request->{'list'} = 'GLOBAL';
    $result = $mj->dispatch($request); 
    &usage($mj, $subs, "Unable to obtain result from Majordomo.") 
      unless $result;
    &Mj::Format::lists($mj, \*STDOUT, \*STDOUT, 'wwwusr', $request, $result);
  }
  elsif ($commands{$func}->[4] eq 'show') {
    $func = $request->{'command'} = 'show';
    $result = $mj->dispatch($request); 
    &usage($mj, $subs, "Unable to obtain result from Majordomo.") 
      unless $result;
    &Mj::Format::show($mj, \*STDOUT, \*STDOUT, 'wwwusr', $request, $result);
  }
  elsif ($commands{$func}->[4] eq 'welcome') {
    &usage($mj, $subs, '');
  }
  elsif ($commands{$func}->[4] eq 'login') {
    &login($mj, $subs, '');
  }
  # Fall through for unexpected cases.
}

#----- Print the Footer -----#
$tmp = $mj->format_get_string('wwwusr', 'foot');
$str = $mj->substitute_vars_format($tmp, $subs);
print STDOUT "$str\n";

#----- Subroutines -----#

sub initialize_log {  
  $ENV{'PATH'} = "/bin:/usr/bin:/usr/ucb";
  umask oct($::UMASK);
  # Change the log level to a higher number (500) for complete debugging
  $::log = new Mj::Log;
  $::log->add
    (
     method      => 'handle',
     id          => 'wwwusr',
     handle      => \*STDERR,
     level       => 50,
     subsystem   => 'mail',
     log_entries => 1,
     log_exits   => 1,
     log_args    => 1,
    );

  $::log->in(20, undef, "info", "Majordomo 2 WWW User Interface - " .
                                 scalar(localtime) .
                                 " from $ENV{'REMOTE_ADDR'}");
  $::log->startup_time();
}

sub login {
  my ($mj, $subs, $message) = @_;
  my ($str, $tmp);

  $subs->{'ERROR'} = $message;
  $::log->message(50, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwusr', 'login');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  $tmp = $mj->format_get_string('wwwusr', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub usage {
  my ($mj, $subs, $message) = @_;
  my ($str, $tmp);

  $subs->{'ERROR'} = $message;
  $::log->message(50, "info", $message) if $message;

  $tmp = $mj->format_get_string('wwwusr', 'welcome');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";

  $tmp = $mj->format_get_string('wwwusr', 'foot');
  $str = $mj->substitute_vars_format($tmp, $subs);
  print STDOUT "$str\n";
  
  exit 1;
}

sub surrender {
  my ($message) = shift;
  $::log->message(50, "info", $message) if $message;
  my ($url) = $cgi->script_name;

  print <<EOM;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN"
"http://www.w3.org/TR/REC-html40/strict.dtd">
<html><head>
<title>Majordomo 2 WWW for Subscribers: Error</title>
</head>
<body>
<h2 align=center>Majordomo Error</h2>
$message
<form method=POST action="$url">
  <input type=hidden name=func value="login">
  Enter the domain name into the box, then press the <b>Sign In</b> button.<br>
  <input name=domain size=45>
  <input type=submit value="Sign In">
</form>
EOM

  if (defined $subs and exists $subs->{'MJOWNER'}) {
    print <<EOM;
Please contact 
<a href="mailto:$subs->{'MJOWNER'}">$subs->{'MJOWNER'}</a>
 for assistance.
EOM
  }

print "</body></html>\n";

  exit 0;
}


=head1 COPYRIGHT

Copyright (c) 2000 Jason Tibbitts for The Majordomo Development
Group.  All rights reserved.

This program is free software; you can redistribute it and/or modify it
under the terms of the license detailed in the LICENSE file of the
Majordomo2 distribution.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the Majordomo2 LICENSE file for more
detailed information.

=cut

#^L
### Local Variables: ***
### cperl-indent-level:2 ***
### End: ***


